      define Vowels [a|e|i|o|u|á|é|í|ó|ú];
      define Cons [b|c|d|f|g|h|j|k|l|m|n|ñ|p|q|r|s|t|v|w|y|z];

      #--------------------------------#
      # allow upper/lower case letters #
      #--------------------------------#

      define Lowercase [A (->) a, B (->) b, C (->) c,D (->) d,E (->) e,F (->) f,G (->) g,H (->) h,I (->) i,J (->) j,K (->) k,L (->) l,M (->) m,N (->) n,O (->) o,P (->) p,Q (->) q,R (->) r,S (->) s,T (->) t,U (->) u,V (->) v,W (->) w,X (->) x,Y (->) y,Z (->) z,Ñ (->) ñ,Ã (->) ã,Ê (->) ê,Î (->) î,Ô (->) ô];

      define UpperCase [ a (->) A,b (->) B,c (->) C,d (->) D,e (->) E,f (->) F,g (->) G,h (->) H,i (->) I,j (->) J,k (->) K,l (->) L,m (->) M,n (->) N,o (->) O,p (->) P,q (->) Q,r (->) R,s (->) S,t (->) T,u (->) U,v (->) V,w (->) W,x (->) X,y (->) Y,z (->) Z,ñ (->) Ñ,ã (->) Ã,ê (->) Ê,î (->) Î,ô (->) Ô];

      ##########################
      #        Ortography      #
      # Normalization Patterns #
      #                        #
      ##########################

      define QUIReplacement [ {ky} (->) {qui} || _ [a|á] ];
      define QUReplacement [ {k} (->) {qu} || _ [e|i|é|í] ,, {k} (->) {qü} || _ [e|i|é|í] ];

      define CReplacement1 [ {k} (->) {c} || _ [a|o|u|á|é|í] ];
      define CReplacement2 [ {k} (->) {c} || [a|o|u|á|é|í] _ .#. ]; ! shac -> shak ; tac -> tak 

      define QYReplacement [ {ky} (->) {qy} || _ [a] ];

      define JReplacement1 [ {h} (->) {j} || Vowels _ Vowels ]; # not accepting sh -> sj 
      define JReplacement2 [ {h} (->) {j} || .#. _ Vowels ]; # changing ja -> ha at the beginning of a word
      define JReplacement3 [ {h} (->) {j} || _ Vowels ]; # changing jemimimi -> jemimimi at the beginning of a word
      define JReplacement4 [ {h} (->) {j} || _ [{ya}|{yi}] ]; # iraanajya -> iraanahya ; aajyitataantsi -> aahyitataantsi

      define ZReplacement1 [ {s} (->) {z} || .#. _ Vowels ];

      define BReplacement1 [ {v} (->) {b} || _ Vowels ];
      define BReplacement2 [ {nv} (->) {mb} || _ Vowels ];
      define WReplacement [ {v} (->) {w} || _ Vowels ];
      define HUReplacement [ {v} (->) {hu} || _ [i|o] ]; ! kuhuinco -> kuvinco, shihuonso -> shivonso

      define PIReplacement [ {py} (->) {pi} || _ [a|á] ];
      define KIReplacement [ {ky} (->) {ki} || _ [a|á] ,, {ky} (->) {qy} || _ [a|á] ]; 
      define RIReplacement [ {ry} (->) {ri} || _ [a|á] ];

      define TIReplacement [ {ty} (->) {ti} || _ [e|é] ];

      define THReplacement [ {ts} (->) {th} || _ [a|e|i|o|á|é|í|ó] ];

      define NYReplacement [ {ny} (->) {ñ} || _ Vowels ];
      define VowelsWithAccent [ {a} (->) {á}, {e} (->) {é}, {i} (->) {í},{o} (->) {ó}, {u} (->) {ú}];

      define ATildeReplacement1 [ {an} (->) {ã} || _ [k|t] ];
      define ATildeReplacement2 [ {am} (->) {ã} || _ [p] ];
      define ETildeReplacement1 [ {en} (->) {ẽ} || _ [k|t] ];
      define ETildeReplacement2 [ {em} (->) {ẽ} || _ [p] ];
      define ITildeReplacement1 [ {in} (->) {ĩ} || _ [k|t] ];
      define ITildeReplacement2 [ {im} (->) {ĩ} || _ [p] ];
      define OTildeReplacement1 [ {on} (->) {õ} || _ [k|t] ];
      define OTildeReplacement2 [ {om} (->) {õ} || _ [p] ];

      define UReplacement1 [ {o} (->) {u} || [Cons] _ [Cons] ]; # ayumpari, kubantsi
      define UReplacement2 [ {o} (->) {u} || [Cons] _ [a|e] ]; # kuakitzi, wuempo , kuaritotzi 
      define UReplacement3 [ {o} (->) {u} || [Cons] _ ]; # paju
      define UReplacement4 [ {o} (->) {u} || .#. _ [Cons] ]; # upempe 

      define NPReplacement [ {mp} (->) {np} || [Vowels] _ [Vowels] ]; # amenpori

      # onset before vowel a 
      define TYOnset1 [ {tey} (->) {ty} || _ [a|á] ]; ! tyaaki 'flower' -> teyaaki 
      define TYOnset2 [ {tiy} (->) {ty} || _ [a|á] ]; ! tyaaki 'flower' -> tiyaaki 
      define PYOnset [ {piy} (->) {py} || _ [a|á] ]; ! pyari 'masato [manioc beer]' -> piyari 
      define SYOnset [ {siy} (->) {sy} || _ [a|á] ]; ! syatatsi 'claw, fingernail' -> siyatatsi 
      define TSYOnset [ {tsiy} (->) {tsy} || _ [a|á] ]; ! tsyairi 'paucar sp.' -> tsiyairi 
      define NYOnset [ {niy} (->) {ny} || _ [a|á] ]; ! kanyari 'green' -> kaniyari 
      define KYOnset [ {kiy} (->) {ky} || _ [a|á] ]; ! chochokyari -> chochokiyari 

      # diphthong: ay 
      # Seen in Ashanĩka do Rio Amônia
      # wayro , DURATIVE suffix 
      define AYDiphthong1 [ {ai} (->) {ay} || [v] _ ];

      define Orthography [ QUIReplacement .o. QUReplacement .o. CReplacement1 .o. CReplacement2 .o. QYReplacement 
.o. AYDiphthong1 
.o. JReplacement1 .o. JReplacement2 .o. JReplacement3 .o. JReplacement4 
.o. ZReplacement1 
.o. BReplacement1 .o. BReplacement2 .o. WReplacement .o. HUReplacement  
.o. PIReplacement .o. KIReplacement .o. RIReplacement .o. TIReplacement .o. THReplacement .o. NYReplacement .o. VowelsWithAccent 
.o. ATildeReplacement1 .o. ATildeReplacement2 .o. ETildeReplacement1 .o. ETildeReplacement2 .o. ITildeReplacement1 .o. ITildeReplacement2 .o. OTildeReplacement1 .o. OTildeReplacement2 
.o. UReplacement1 .o. UReplacement2 .o. UReplacement3 .o. UReplacement4  
.o. NPReplacement 
.o. TYOnset1 .o. TYOnset2 .o. PYOnset .o. KYOnset 
]; 


