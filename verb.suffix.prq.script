define EP [
  "[--][-t][+EP]" : "@EP@"[{t}]
| "[--][-tz][+EP]" : "@EP@"[{tz}]
| "[--][-a][+EP]" : "@EP@"[{a}]
];

# ****************************************
                   
define V=S=PROG [
  "[--][-atz][ASP][+PROG]" : "@EP"[{atz}] 
| "[--][-atzi][ASP][+PROG]" : "@EP"[{atzi}] 
| "[--][-atsi][ASP][+PROG]" : "@EP"[{atsi}] 
| "[--][-aty][ASP][+PROG]" : "@EP"[{aty}]
| "[--][-ati][ASP][+PROG]" : "@EP"[{ati}]
| "[--][-atiy][ASP][+PROG]" : "@EP"[{atiy}]
];
                   
define PROG [ [EP V=S=PROG]|[V=S=PROG] ];
                   
# ****************************************
                   
define V=S=PRNO=SET1 [
  "[--][-ri][+3m.O]" : [{ri}] 
| "[--][-re][+3m.O]" : [{re}] 
| "[--][-rii][+3m.O]" : [{rii}] 
| "[--][-ro][+3n.m.O]" : [{ro}] 
| "[--][-vo][+3n.m.O]" : [{vo}] 
];
define V=S=PRNO=SET2 [
# ----------------------------------
  "[--][-ai][+1INCL.PL.O]" : "@EP"[{ai}] 
| "[--][-ae][+1INCL.PL.O]" : "@EP"[{ae}] 
];
                   
define PRNO [ [V=S=PRNO=SET1]|[EP V=S=PRNO=SET2]|[V=S=PRNO=SET2] ];
                   
# ****************************************
                   
define V=S=DIST [
  "[--][-ye][+DIST]" : "@EP"[{ye}]
| "[--][-yee][+DIST]" : "@EP"[{yee}]
| "[--][-yi][+DIST]" : "@EP"[{yi}]
];
                   
define DIST [ [EP V=S=DIST]|[V=S=DIST] ];
                   
# ****************************************
                   
define V=S=DIR [
# 
  "[--][-av+object.oriented][Adv][+DIR][=contrary,opposite]" : "@EP"[{av}] 
| "[--][-aav+object.oriented][Adv][+DIR][=contrary,opposite]" : "@EP"[{aav}] 
# ablative case
# 1) inchoative (INCH): beginning of a new state.
# 2) inceptive (INCEP): beginning of a new action.
# 3) continuing action in a series of the actor's activities.
| "[--][-an+source][Adv][+DIR][=(upon).leaving/departing;INCH:beginning.of.a.new.state;INCEP:beginning.of.a.new.action]" : "@EP"[{an}] 
# allative 'adlativo' case
# 1) indicates the final phase of an action/state.
# iporekapaake aka / i- =porek -ap -aak -e ** =aka+proximal.b. / 3m.s- =to.shine -dir[on.arrival] -prf -irr ** =here / EN: 'they.arrive.here.like.flashes.of.lightning; lit.: they.shine.on.arrival.here'; ES: 'aquí llegan.como.destellos.de.relampago'
| "[--][-ap+goal][Adv][+DIR][=on.arrival,arriving;final.phase.of.an.action/state]" : "@EP"[{ap}]
| "[--][-apa+goal][Adv][+DIR][=on.arrival,arriving;final.phase.of.an.action/state]" : "@EP"[{apa}]
];
                   
define DIR [ [EP V=S=DIR]|[V=S=DIR] ];
                   
# ****************************************
                   
define V=S=PRF [
  "[--][-ak][ASP][+PRF]" : "@EP"[{ak}] 
| "[--][-aak][ASP][+PRF]" : "@EP"[{aak}] 
];
                   
define PRF [ [EP V=S=PRF]|[V=S=PRF] ];
                   
# ****************************************
                   
# subjunctive clausal enclitic 
# SUBJUNCT, SUBJ  
define V=S=OPT [
  "[--][=ta][+OPT_SUBJ]" : [{ta}]
];
                   
# ****************************************
                   
define V=S=PL [
  "[--][-he][Num][+PL]" : "@EP"[{he}] 
| "[--][-hei][Num][+PL]" : "@EP"[{hei}] 
| "[--][-hee][Num][+PL]" : "@EP"[{hee}] 
# -ite only appears in nominalized verb structures 
# ---------------------------------------
# ichiki / i- =chik -i / 3m.s- =to.cut -real / EN: 'he cut'
# ichikaiyini / i- =chik -aiy -i -ni / 3m.s- =to.cut -pl -real -pl+circumfixing / EN: 'they cut'
| "[--][-aiy][+PL]" : "@EP"[{aiy}] 
| "[--][-aey][+PL]" : "@EP"[{aey}] 
];
                   
define PL [ [EP V=S=PL]|[V=S=PL] ];
                   
# ****************************************
                   
define V=S=PLcircumfixing [
  "[--][-ni+circumfixing][+PL]" : [{ni}]
| "[--][-ne+circumfixing][+PL]" : [{ne}]
];
                   
# ****************************************
                   
! I am taking Aspect instead of Cas as it is an analysis made specifically for Ashaninka.
! It specifies an event as a whole, that may be: 
! resolved.(event): finality-and-closure, once-and-for.all 
! regressive.(event): being.back 
define V=S=REGR [
# eiro aneahiritsi / =eiro ** a- =ne -ah -i -ri -tsi / =NEG.IRR ** 1pl.a- =to.see -term[finality-and-closure] -real -3m.o -neg / ; EN: 'we.won't.see.him.again'; ES: 'No.lo.veremos.otra.vez' 
  "[--][-a][Asp][+REGR_TERM][=REGR: to.repeat.sth.again, to.continue.sth.; TERM: finality-and-closure, once-and-for.all]" : "@EP"[{a}]
| "[--][-ah][Asp][+REGR_TERM][=REGR: to.repeat.sth.again, to.continue.sth.; TERM: finality-and-closure, once-and-for.all]" : "@EP"[{ah}]
| "[--][-aha][Asp][+REGR_TERM][=REGR: to.repeat.sth.again, to.continue.sth.; TERM: finality-and-closure, once-and-for.all]" : "@EP"[{aha}]
];
                   
define REGR [ [EP V=S=REGR]|[V=S=REGR] ];
                   
# ****************************************
                   
define V=S=CNTF [
  "[--][-mi][+CNT.F]" : [{mi}]
];
                   
# ****************************************
                   
# -------------------------------------------
define V=S=REAL [
  "[--][-e][+REAL]" : "@EP"[{e}]
| "[--][-ee][+REAL]" : "@EP"[{ee}]
| "[--][-i][+REAL]" : "@EP"[{i}]
| "[--][-a][+REAL]" : "@EP"[{a}]
# -------------------------------------------
| "[--][-ia][+REAL]" : "@EP"[{ia}]
| "[--][-ya][+REAL]" : "@EP"[{ya}]
| "[--][-yaa][+REAL]" : "@EP"[{yaa}]
];
      
define V=S=IRR [
  "[--][-e][+IRR]" : "@EP"[{e}]
| "[--][-ee][+IRR]" : "@EP"[{ee}]
| "[--][-ia][+IRR]" : "@EP"[{ia}]
| "[--][-ya][+IRR]" : "@EP"[{ya}]
| "[--][-ye][+IRR]" : "@EP"[{ye}]
| "[--][-yea][+IRR]" : "@EP"[{yea}]
| "[--][-a][+IRR]" : "@EP"[{a}]
| "[--][-i][+IRR]" : "@EP"[{i}]
| "[--][-yaa][+IRR]" : "@EP"[{yaa}]
| "[--][-ea][+IRR]" : "@EP"[{ea}]
];
      
define V=S=REALRFL [
  "[--][-a][+REAL.RFL]" : "@EP"[{a}]
];
      
define V=S=IRRRFL [
  "[--][-ia][+IRR.RFL]" : "@EP"[{ia}]
# nontaiya / no- N- =ta -iya / 1sg.s- irr- =to.burn -irr.refl / ES: 'yo.me.quemaré' 
| "[--][-eya][+IRR.RFL]" : "@EP"[{eya}]
| "[--][-iya][+IRR.RFL]" : "@EP"[{iya}]
];
# -------------------------------------------
                   
define STATUS [
  [EP V=S=REAL]|[V=S=REAL]
| [EP V=S=IRR]|[V=S=IRR]
| [EP V=S=REALRFL]|[V=S=REALRFL]
| [EP V=S=IRRRFL]|[V=S=IRRRFL]
];
                   
# ****************************************
                   
define V=S=AUG [
  "[--][-piro][+AUG]" : "@EP"[{piro}]
| "[--][-pero][+AUG]" : "@EP"[{pero}]
| "[--][-pira][+AUG]" : "@EP"[{pira}]
# -------------------------------------------
| "[--][-ni][+AUG]" : "@EP"[{ni}]
];
                   
define AUG [ [EP V=S=AUG]|[V=S=AUG] ];
                   
# ****************************************
                   
! it specifies a situation which lasted for a period of time in the past.
!*) Nojate nomanaveete / Nohate nomanaveete / no- =ha -t -e  no- =mana -vee -t -e / 1sg.s- =to.go -ep -irr  1sg.s- =to.shoot -dur -ep -irr / EN: "I'm.going.to.hunt"; ES: 'yo.voy.a.cazar'  
! 
define V=S=DUR [
  "[--][-vai][Asp][+DUR]" : "@EP"[{vai}] 
| "[--][-vae][Asp][+DUR]" : "@EP"[{vae}] 
| "[--][-vee][Asp][+DUR]" : "@EP"[{vee}] 
| "[--][-vei][Asp][+DUR]" : "@EP"[{vei}] 
];
                   
define DUR [ [EP V=S=DUR]|[V=S=DUR] ];
                   
# ****************************************
                   
!      define V=S=3O [
!       "[--][+3O]" : "@EP"[{ni}]
!      ];
                   
# ****************************************
                   
!define V=S=BEN [
!  "[--][-vint][+BEN][=action.done.for.sb.else]" : "@EP"[{vint}]
!| "[--][-vent][+BEN][=action.done.for.sb.else]" : "@EP"[{vent}]
!];
                   
# ****************************************
                   
!      define V=S=ADVP [
!       "[--][+ADV.P]" : "@EP"[{ranki}]
!      ];
                   
# ****************************************
                   
# ---------------------------------------------------------------
# subordinator 
# adverbializer: subordinating conjunction that links a subordinate clause to a main clause, and
# indicates that the subordinate clause has an "adverbial" or interpropositional relation to the main clause, indicating
# purpose, condition, time, and location. (SIL) 
define V=S=ADV [
  "[--][-ka+proximal.loc.][+ADV]" : "@EP"[{ka}] 
| "[--][-ra+medial.loc.][+ADV]" : "@EP"[{ra}] 
| "[--][-nta+distal.loc.][+ADV]" : "@EP"[{nta}] 
# ---------------------------------------------------------------
| "[--][-ranki+fused.t.][+ADV.P]" : "@EP"[{ranki}] 
# ---------------------------------------------------------------
| "[--][-ni][AS][+ADV]" : "@EP"[{ni}] 
| "[--][-ini][AS][+ADV]" : "@EP"[{ini}] 
| "[--][-eni][AS][+ADV]" : "@EP"[{eni}] 
];
                   
define ADV [ [EP V=S=ADV]|[V=S=ADV] ];
                   
# ****************************************
                   
define V=S=ADV=SET2 [
  "[--][-paite][+ADV][=during, at.the.time.of.sth., period]" : "@EP"[{paite}] 
| "[--][-paiti][+ADV][=during, at.the.time.of.sth., period]" : "@EP"[{paiti}] 
];
                   
define ADV=SET2 [ [EP V=S=ADV=SET2]|[V=S=ADV=SET2] ];
                   
# ****************************************
                   
define V=S=O [
  "[--][-na][+1SG.O]" : [{na}] 
| "[--][-mi][+2SG.O]" : [{mi}] 
| "[--][-mpi][+2SG.O]" : [{mpi}] 
| "[--][-npi][+2SG.O]" : [{npi}] 
| "[--][-ni][+3SG.O]" : [{ni}] 
| "[--][-ne][+3SG.O]" : [{ne}] 
];
                   
# ****************************************
                   
! -kari EN: 'in.order.to.prevent.sth.from.happening'; ES: 'para.que.no; no.sea.(que); sino' 
! -kari EN: 'used.to.introduce.the.reason.for.the.particular.emotion.mentioned'; ES: 'con.el.fin.de' 
define V=S=APPR [
# omitsitsiyimikari / o- =mitsitsiy -i -mi -kari / 3n.m.s- = / ES: 'para.que.no.te.hundas'  
# *) pintzinai, anankorimikari shenotsi / pi- N- =tzin -a -i *** =anankori -mi -kari *** =shenotsi / 2s- irr- =to.rise -term -irr *** =to.cross -2sg.o -appr *** =goiter  
#    EN: 'get up! lest you cross the goiter'; ES: 'levántate, sino te va a cruzar la mamá del bocio' 
  "[--][-kari][+APPR][=lest (in.order.to.prevent)]" : [{kari}]
];
                   
# ****************************************
                   
!      define V=S=2A [
!       "[--][+2A]" : "@EP"[{pi}|{p}]
!      ];
                   
# ****************************************
                   
define V=S=REL [
  "[--][-ri][+REL]" : [{ri}]
# -----------------------------------------------
| "[--][-ni][+REL.IRR]" : [{ni}]
| "[--][-ne][+REL.IRR]" : [{ne}] ! =kaari =kemisan-t-atsi-ne ESP: 'terco', EN: 'stubborn' 
];
                   
# ****************************************
                   
define V=S=FRUS [
  "[--][-ve][+FRUS]" : "@EP"[{ve}]
| "[--][-vi][+FRUS]" : "@EP"[{vi}]
| "[--][-vit][+FRUS]" : "@EP"[{vit}]
# -----------------------------------------------
| "[--][-macheve][+FRUS]" : "@EP"[{macheve}]
];
                   
define FRUS [ [EP V=S=FRUS]|[V=S=FRUS] ];
                   
# ****************************************
                   
define V=S=APPLREAS [
  "[--][-ant][+APPL.REAS]" : "@EP"[{ant}]
| "[--][-anta][+APPL.REAS]" : "@EP"[{anta}]
];
                   
define APPLREAS [ [EP V=S=APPLREAS]|[V=S=APPLREAS] ];
                   
# ****************************************
                   
define V=S=HAB [
  "[--][-apin][+HAB_CUST][=always]" : "@EP"[{apin}] 
| "[--][-apiin][+HAB_CUST][=always]" : "@EP"[{apiin}] 
| "[--][-apinin][+HAB_CUST][=always]" : "@EP"[{apinin}] 
| "[--][-apini][+HAB_CUST][=always]" : "@EP"[{apini}]
];
                   
define HAB [ [EP V=S=HAB]|[V=S=HAB] ];
                   
# ****************************************
                   
# --------------------------------------
#           Impersonal Passive 
# --------------------------------------
define V=S=IMPP [
# ichikaitakiro / i- =chik -ai -t -ak -i -ro / 3m.a- =to.cut -imp.p -ep -prf -real -3n.m.o / EN: 'someone.cut.it; it.was.cut'  
# hiitaitziri / h- =ii -t -ai -tz -i -ri / 3m.a- =to.be.called -ep -imp.p -ep -real -3m.o / EN: 'that.which.is.named'  
# impishitaitiro / i- N- =pishi -t -ai -t -i -ro / 3m.a- irr- =to.sweep -ep -imp.p -ep -real -3n.m.o / EN: 'someone.swept.it; it.was.swept'  
# ikitsataitakina / i- =kitsa -t -ai -t -ak -i -na / 3m.a- =to.dress -ep -imp.p -ep -prf -real -1sg.o / EN: 'they.dressed.me; I.was.dressed' 
# ishinkitaitakiri / i- =shinki -t -ai -t -ak -i -ri / 3m.a- =to.get.drunk -ep -imp.p -ep -prf -real -3m.o / EN: 'they.made.him.drunk', es: 'le.hicieron.emborrachar'  
# Only with 3rd person ???  
  "[--][-ai][+IMP.P]" : "@EP"[{ai}] 
| "[--][-ae][+IMP.P]" : "@EP"[{ae}] ! yet to be tested 
| "[--][-ee][+IMP.P]" : "@EP"[{ee}] ! yet to be tested 
# these are incorrect ???
! | "[--][-t][+IMP.P]" : "@EP"[{t}] 
! | "[--][-tz][+IMP.P]" : "@EP"[{tz}] 
];
                   
define IMPP [ [EP V=S=IMPP]|[V=S=IMPP] ];
                   
# ****************************************
                   
# *) -pameneribe jipajitari yora? / -pamenerive hipahitari yora? / p- =amen -e -ri -ve *** hi- =pahi -t -a -ri *** =yora+m.+medial
#     2a- =to.look -real -3m.o =excl[exclaiming] *** 3m.a- =to.be.called -ep -irr -3m.o *** =that / EN: "look, what's that?"  
define V=S=EXCL [
  "[--][-ve][+EXCL][=exclaiming,announcing]" : [{ve}]
| "[--][-vi][+EXCL][=exclaiming,announcing]" : [{vi}]
];
                   
# ****************************************
                   
define V=S=STAT=SET1 [
# -------- STATIVITY MARKERS -------------
# -------- STATIVITY IMPERFECTIVE -------------
! koiteatsiri / =koite -atsi -ri / =to.be.in.need -stat -rel / EN: 'poor'; ES: 'pobre' 
! betsikayetatsiri / vetsikayetatsiri / =vetsik -a -ye -t -atsi -ri / =to.create -ep -dist -ep -stat -rel / EN: 'invention (thing/idea.that.has.been.invented)'; ES: 'invento'  
  "[--][-atsi][+STAT]" : "@EP"[{atsi}]
! taachari / =ta -acha -ri / =to.burn -stat -rel / EN: 'the.burned.one', ES: 'quemado'
! nashitachari / =nashi -t -acha -ri / =to.separate -ep -stat -rel / EN: 'different.one'; ES: 'diferente' 
! ashinonkaachari / =ashinonk -aacha -ri / =to.suffer -stat -rel / EN: 'poor'; ES: 'pobre' 
! shirontakaantachari / =shiron -t -aka -ant -acha -ri / =to.laugh.at.sb. -ep -caus.soc -appl.inst -stat -rel / EN: 'funny, joker'; ES: 'chistoso'
! kantakotashitantachari / =kant -ako -t -ashi -t -ant -acha -ri / =to.say -appl -ep -appl.int -appl.inst -stat -rel / EN: 'accuser, traitor'; ES: 'acusador, traidor'; 
! oyaakotachari / =oya -ako -t -acha -ri / =to.follow -appl -ep -stat -rel / EN: 'dependent'; ES: 'dependiente'
! katikachari / =katik -acha -ri / =to.hang -stat -rel / EN: 'hanged'; ES: 'ahorcado'  
! pahitachari Pityotsi / =pahi -t -acha -ri =Pityotsi / =to.be.called -ep -stat -rel =proper.name / ES: 'who.is.called.Pityotsi'  
| "[--][-acha][+STAT]" : "@EP"[{acha}] 
| "[--][-aacha][+STAT]" : "@EP"[{aacha}] 
# -------- TEMPORAL STATIVITY MARKERS ------
# -------- STATIVE PERFECTIVE  -------------
! kenanaintsiri / =ken -an -aintsi -ri / =to.walk -dir+leaving -stat -rel / EN: 'that.walks.past.sb.'; ES: 'que.va.caminando' 
! jibataintsiri / =hivataintsiri / =hivat -aintsi -ri / =to.get.ahead -stat -rel / EN: 'first.one, the.one.that.has_gotten_ahead/starts'; ES: 'primero, el.que.está.adelante' 
! impoitaintsiri / =impoi -t -aintsi -ri / =to.follow.behind -ep -stat -rel / EN: 'the.one.that.follows.behind, last.one' ; ES: 'lo.que.viene.atrás, último' 
! abisaintsiri / avisaintsiri / =avis -aintsi -ri / =to.pass -stat -rel / EN: 'what.has.happened'; ES: 'lo.que.ha.ocurrido' 
! sabikaintsiri / savikaintsiri / =savik -aintsi -ri / =to.sit -stat -rel / EN: 'sitted'; ES: 'sentado' 
! abisaintsiri peerani / avisaintsiri peerani / =avis -aintsi -ri =peerani / =to.pass -stat -rel =long.ago / EN: 'history'; ES: 'historia; lo.que.sucedió.hace.mucho.tiempo'  
| "[--][-aintsi][+STAT]" : "@EP"[{aintsi}] 
| "[--][-aentsi][+STAT]" : "@EP"[{aentsi}] 
# --------
! inchato tainchari / =incha -to =ta -incha -ri / =plant -cl:tree =to.burn -stat -rel / ES: 'árbol.quemado'; EN: 'burned.tree' 
! kaniri pichainchari / =kaniri =picha -incha -ri / =yucca =to.crush.sth. -stat -rel / ES: 'yuca.(masa.cocinada)'; EN: 'cooked.yucca.mass' 
! apatotainchari / =apato -t -aincha -ri / =to.meet -ep -stat -rel / ES: 'grupo.de.gente, reunión'; EN: 'meeting' 
! vakoshi tononkainchari / =vako -shi =tononk -aincha -ri / =venomous.plant.sp. -cl:leaf =to.grind.sth. -stat -rel / ES: 'el.veneno.huaco.de.las.hojas.molidas'; EN: 'ground.leaf.from.a.venomous.plant' 
! aparoni tsatainchari / =aparoni =tsat -aincha -ri / =one =to.tear.into.two.parts -stat -rel / ES: 'uno.colgado'; EN: 'one.torn.into.two.parts' 
! noetsaare kityonkari oobaenchari / no- =ketsaa -re =kityonka -ri =oob -aencha -ri / 1sg.poss- =dress -nmz =to.be.red -adj =to.put.sth.out.in.the.sun -stat -rel / ES: 'el.vestido.rojo.que.está.soleado'; EN: 'the.red.dress.that.was.put.out.in.the.sun' 
! Bankanainchari / vankanainchari / =vank -an -aincha -ri / =to.lift.sth.up -dir+departing -stat -rel / EN: 'that.has.been.lifted.up.(departing)'; ES: 'que.ha.subido' 
| "[--][-incha][+STAT]" : "@EP"[{incha}]
| "[--][-aincha][+STAT]" : "@EP"[{aincha}]
| "[--][-aencha][+STAT]" : "@EP"[{aencha}]
];

define V=S=STAT=SET2 [
# -------- STATIVITY MARKERS -------------
# -------- STATIVITY IMPERFECTIVE -------------
  "[--][-ach][+STAT]" : "@EP"[{ach}] 
# -------- TEMPORAL STATIVITY MARKERS ------
# -------- STATIVE PERFECTIVE  -------------
| "[--][-aints][+STAT]" : "@EP"[{aints}] 
| "[--][-aits][+STAT]" : "@EP"[{aits}] 
];

                   
define STAT [ [EP V=S=STAT=SET1]|[V=S=STAT=SET1] ];
                   
# ****************************************
                   
define V=S=STATT [ # revise!
  "[--][-aintsi][+STAT.T]" : "@EP"[{aintsi}]
];
                   
# ****************************************
                   
define V=S=STATIPRF [ # revise!
  "[--][-aintsi][+STAT.IPRF]" : "@EP"[{aintsi}]
| "[--][-atsi][+STAT.IPRF]" : "@EP"[{atsi}]
];
                   
# ****************************************
                   
define V=S=STATPRF [ # revise!
  "[--][-aintsi][+STAT.PRF]" : "@EP"[{aintsi}]
];
                   
# ****************************************
                   
define V=S=Q [
# ampokaika / a- N- =pok -ai -ka / 1pl.s- irr- =to.come -real -q / EN: 'we will come back (when)?'  
  "[--][-ka][+Q]" : [{ka}]
];
                   
# ****************************************
                   
define V=S=APPL [
  "[--][-ako][+APPL.GEN][=with.reference.to.sth.; about.sth.]" : "@EP"[{ako}]
];
                   
define APPL [ [EP V=S=APPL]|[V=S=APPL] ];
                   
# ****************************************
                   
define V=S=ABS [ # revise!
  "[--][-tsi][+ABS]" : "@EP"[{tsi}]
| "[--][-ntsi][+ABS]" : "@EP"[{ntsi}]
];
                   
# ****************************************
                   
!define V=S=noun [
!  "[--][-ani][+noun][=child]" : "@EP"[{ani}]
!| "[--][-miront][+noun][=flat]" : "@EP"[{miront}]
!];
                   
# ****************************************
                   
define V=S=COND [
  "[--][-rika][+COND][=if (expresses.possible.condition); like (comparison)]" : [{rika}] ! +evidential 
# This suffix indicates, for good/bad, an action or idea wasn't fulfilled.
# Also abbreviated CNT.EXP 'counter-expectational' 
| "[--][-me][+SUBJ]" : [{me}] 
];
                   
# ****************************************
                   
!define V=S=disheveledhead [
!  "[--][+disheveled.head]" : "@EP"[{menkotzi}] ! menkoti, menkotsi
!];
                   
# ****************************************
                   
define V=S=DIM [ # revise!
  "[--][-nint][+DIM][=almost,a.little.bit]" : "@EP"[{nint}]
| "[--][-ni][+DIM]" : "@EP"[{ni}]
| "[--][-ini][+DIM]" : "@EP"[{ini}]
];
                   
# ****************************************
                   
!define V=S=CL [
!  "[--][-pooki][+CL:quantity][=group,bunch]" : "@EP"[{pooki}]
!| "[--][-poroki][+CL:quantity][=group,bunch]" : "@EP"[{poroki}]
!| "[--][-ki][+CL:S.R.H][=small,round;hard]" : "@EP"{ki} 
!];
                   
# ****************************************
                   
!define V=S=3mposs [
!  "[--][+3m.poss]" : "@EP"[{i}|{y}]
!];
                   
# ****************************************
                   
define V=S=NMZ=SET1 [ # revise!
# ----------------------------------
  "[--][-rontsi][+NMZ]" : "@EP"[{rontsi}] 
| "[--][-vontsi][+NMZ]" : "@EP"[{vontsi}] 
| "[--][-rintsi][+NMZ]" : "@EP"[{rintsi}] 
| "[--][-rentsi][+NMZ]" : "@EP"[{rentsi}] 
| "[--][-renti][+NMZ]" : "@EP"[{renti}] 
# ----------------------------------
| "[--][-ri][+NMZ]" : "@EP"[{ri}] 
! 
| "[--][-nk][+NMZ]" : "@EP"[{nk}] 
| "[--][-nka+quality][+NMZ]" : "@EP"[{nka}] 
| "[--][-enkaa+quality][+NMZ]" : "@EP"[{enkaa}] 
| "[--][-enka+quality][+NMZ]" : "@EP"[{enka}] 
| "[--][-inka+quality][+NMZ]" : "@EP"[{inka}] 
! 
| "[--][-ntsi][+NMZ]" : "@EP"[{ntsi}] 
| "[--][-mento+n.m.][NMZ][+CONCR]" : "@EP"[{mento}]
| "[--][-minto+n.m.][NMZ][+CONCR]" : "@EP"[{minto}]
];

define V=S=NMZ=SET2 [
# ----------------------------------
  "[--][-aantsi][NMZ][+INF]" : "@EP"[{aantsi}] 
| "[--][-antsi][NMZ][+INF]" : "@EP"[{antsi}] 
| "[--][-agantsi][NMZ][+INF]" : "@EP"[{agantsi}] 
];
                   
define NMZ [
  [V=S=NMZ=SET1]
| [EP V=S=NMZ=SET2]|[V=S=NMZ=SET2]
];
                   
# ****************************************
                   
define V=S=ADJ [ # revise!
# ----------------------------------
  "[--][-ri+m.][NMZ][+ADJ]" : "@EP"[{ri}] 
| "[--][-re+m.][NMZ][+ADJ]" : "@EP"[{re}] 
| "[--][-ro+n.m.][NMZ][+ADJ]" : "@EP"[{ro}] 
| "[--][-vo+n.m.][NMZ][+ADJ]" : "@EP"[{vo}] 
# ----------------------------------
! nyaatsatinkaro perota / =nyaatsa -tinkaro =pelota / =to.play -adj =ball / EN: 'female.soccer.player', ES: 'jugadora.de.fútbol' 
| "[--][-tsinkari+m.][NMZ][+ADJ]" : "@EP"[{tsinkari}] 
| "[--][-tzinkari+m.][NMZ][+ADJ]" : "@EP"[{tzinkari}] 
| "[--][-tinkari+m.][NMZ][+ADJ]" : "@EP"[{tinkari}] 
| "[--][-tsinkaro+n.m.][NMZ][+ADJ]" : "@EP"[{tsinkaro}] 
| "[--][-tzinkaro+n.m.][NMZ][+ADJ]" : "@EP"[{tzinkaro}] 
| "[--][-tinkaro+n.m.][NMZ][+ADJ]" : "@EP"[{tinkaro}] 
# ----------------------------------
! atsanaantinkari / =atsana -ant -tinkari / =to.take.advantage.of.sth/sb -appl.inst -adj / EN: 'that.who.exists.to.take.advantage.of.sb., abuser'; ES: 'abusador'  
! yotantinkari / =yot -ant -tinkari / =to.know -appl.inst -adj / EN: 'that.one.who.exists.to.know'; ES: '...'  
| "[--][-ant][+APPL.INST][--][-tinkari+m.][NMZ][+ADJ]" : "@EP"[{antinkari}] 
| "[--][-ant][+APPL.INST][--][-tzinkari+m.][NMZ][+ADJ]" : "@EP"[{antzinkari}] 
| "[--][-ant][+APPL.INST][--][-tzinkaro+n.m.][NMZ][+ADJ]" : "@EP"[{antzinkaro}] 
# ---------PARTICIPANT--------------
| "[--][-ntzi+m.+partic.][NMZ][+ADJ]" : "@EP"[{ntzi}] 
| "[--][-ntsi+m.+partic.][NMZ][+ADJ]" : "@EP"[{ntsi}] 
| "[--][-nti+m.+partic.][NMZ][+ADJ]" : "@EP"[{nti}] 
| "[--][-nto+n.m.+partic.][NMZ][+ADJ]" : "@EP"[{nto}] 
# ---------CAPABLE--------------
! ryaa-ntzi, ryaa-nto 
! sonkareanti / =sonka -reanti / =to.play.antara -adj / EN: 'musician', ES: 'músico'
! sananti shimarianti / =sananti =shima -rianti / =heron =to.fish -adj / EN: 'fisher.heron'; ES: 'garza.pescadora' 
! shirampari shimarianti / =shirampari =shima -rianti / =man =to.fish -adj / EN: 'fisherman'; ES: 'hombre.pescador' 
! antavairyanto / =ant -a -vai -ry -a -nto / [=to.do -ep -dur]:to.work -nmz -ep -adj / EN: 'female.hard.worker' 
| "[--][-ryaantzi+m.+capable][NMZ][+ADJ]" : "@EP"[{ryaantzi}] 
! tsameeyaanti / =tsamee -yaanti / =to.cultivate -yaanti+m.+capable / EN: 'planter'; ES: 'cultivador' 
| "[--][-yaanti+m.+capable][NMZ][+ADJ]" : "@EP"[{yaanti}] ! only 1 ocurrence in corpus 
| "[--][-rianti+m.+capable][NMZ][+ADJ]" : "@EP"[{rianti}] 
| "[--][-riantzi+m.+capable][NMZ][+ADJ]" : "@EP"[{riantzi}] 
| "[--][-reanti+m.+capable][NMZ][+ADJ]" : "@EP"[{reanti}] 
! 
| "[--][-ryaanto+n.m.+capable][NMZ][+ADJ]" : "@EP"[{ryaanto}] 
| "[--][-ryanto+n.m.+capable][NMZ][+ADJ]" : "@EP"[{ryanto}] 
| "[--][-rianto+n.m.+capable][NMZ][+ADJ]" : "@EP"[{rianto}] 
];
                   
# ****************************************
                   
# --------Sociative-Causative ----------
define V=S=CAUSSOC [
  "[--][-ak][+CAUS.SOC]" : "@EP"[{ak}]
| "[--][-aka][+CAUS.SOC]" : "@EP"[{aka}]
| "[--][-akaa][+CAUS.SOC]" : "@EP"[{akaa}]
];
                   
define CAUSSOC [ [EP V=S=CAUSSOC]|[V=S=CAUSSOC] ];
                   
# ****************************************
                   
!define V=S=APPLSOC [
!  "[--][-ak][+APPL.SOC]" : "@EP"[{ak}]
!];
                   
# ****************************************
                   
!define V=S=CAUSCOM [
!  "[--][-aka][+CAUS.COM]" : "@EP"[{aka}]
!];
                   
# ****************************************
                   
!define V=S=CAUS [
!  "[--][-ak][+CAUS]" : "@EP"[{ak}]
!| "[--][-aka][+CAUS]" : "@EP"[{aka}]
!];
                   
# ****************************************
                   
define V=S=APPLINST [
  "[--][-ant][+APPL.INST]" : "@EP"[{ant}]
];
                   
define APPLINST [ [EP V=S=APPLINST]|[V=S=APPLINST] ];
                   
# ****************************************
                   
# -----------------------------------------------------
# Valence-adjusting operators, degree, time and manner 

! kivantapaintairi / =kivant -apaint -ai -ri / =to.visit -punctual[short-term] -irr -rel / EN: 'short-term.visitor/visit'; ES: ''   
! saikapaintziri / =saik -apaint -tz -i -ri / =to.be -punctual[short-term] -ep -irr -rel / EN: 'short-term resident/stay'; ES: ''   
! 
! yaapanantziri / y- =a -apanant -tz -i -ri / 3m.a- =to.take -punctual[once.more] -ep -irr -3m.o / EN: 'he.grabbed.it.once.more' 
define V=S=SEMEL [
# semelfactive: 
  "[--][-apaint][+PUNCTUAL][=once;for.a.while,short-term]" : "@EP"[{apaint}]
| "[--][-apaent][+PUNCTUAL][=once;for.a.while,short-term]" : "@EP"[{apaent}]
# -----------------------------------------------------
# semeliterative: single repetition of event/action (semel: one.time) 
| "[--][-apanant][+ITERATIVE][=one.more.time,once.more]" : "@EP"[{apanant}]
| "[--][-apan][+ITERATIVE][=one.more.time,once.more]" : "@EP"[{apan}]
];
                   
define SEMEL [ [EP V=S=SEMEL]|[V=S=SEMEL] ];
                   
# ****************************************
                   
define V=S=REP [
  "[--][-a][+REP][=again.and.again]" : "@EP"[{a}]
| "[--][-aa][+REP][=again.and.again]" : "@EP"[{aa}]
];
                   
define REP [ [EP V=S=REP]|[V=S=REP] ];
                   
# ****************************************
                   
!define V=S=forawhile [
!  "[--][+for.a.while]" : "@EP"[{apaint}]
!];
                   
# ****************************************
                   
define V=S=DIMDEGR [
  "[--][-nint][+DIM.DEGR][=almost,a.little.bit]" : "@EP"[{nint}]
];
                   
define DIMDEGR [ [EP V=S=DIMDEGR]|[V=S=DIMDEGR] ];
                   
# ****************************************
# -----------------------------------------------------
      
# purpusive: action.done.with.some.purpose.in.view 
define V=S=APPLINT [
  "[--][-ashi][+APPL.INT][=purpose,(with.the).intent]" : "@EP"[{ashi}]
| "[--][-shi][+APPL.INT][=purpose,(with.the).intent]" : "@EP"[{shi}]
];
                   
define APPLINT [ [EP V=S=APPLINT]|[V=S=APPLINT] ];
                   
# ****************************************
                   
define V=S=LOC [
  "[--][-ki][+LOC]" : "@EP"[{ki}]
];
                   
# ****************************************
                   
define V=S=NCFOC [
  "[--][=tya][+N.C.FOC][=urgency, impatience, surprise, exasperation (non-contradictory.emphasis); almost (with.time.adverbs)]" : [{tya}]
];
                   
# ****************************************
                         
define V=S=OFOC [
  "[--][=kya][+O.FOC]" : [{kya}] 
| "[--][=kia][+O.FOC]" : [{kia}] 
| "[--][=kea][+O.FOC]" : [{kea}] 
];
                   
# ****************************************
                         
!define V=S=poss [
!  "[--][+poss]" : "@EP"[{ni}|{te}]
!];
                   
# ****************************************
                         
define V=S=EMPH [
# tya: urgency, impatience, surprise, exasperation 
# aritya / =ari -tya / =PP -EMPH /  
  "[--][-tya][+EMPH][=urgency, impatience, surprise, exasperation (non-contradictory.emphasis); almost (with.time.adverbs)]" : [{tya}]
! 
| "[--][-kya][+EMPH]" : [{kya}]
| "[--][-kia][+EMPH]" : [{kia}]
| "[--][-kea][+EMPH]" : [{kea}]
! 
| "[--][-poo][+EMPH]" : "@EP"[{poo}]
! 
| "[--][-ha][+EMPH][=contradictory.emphasis]" : "@EP"[{ha}]
! 
| "[--][-tzi][+EMPH]" : "@EP"[{tzi}]
! 
| "[--][-sa][+EMPH]" : "@EP"[{sa}]
];
                   
# ****************************************
                         
!define V=S=MCAUS [
!  "[--][-mi][+M.CAUS]" : "@EP"[{mi}]
!];
                   
# ****************************************
                         
!      define V=S=CAUSAGT [
!       "[--][+CAUS.AGT]" : "@EP"[{v}]
!      ];
                   
# ****************************************
                               
define V=S=APPLBEN [
  "[--][-vint][+APPL.BEN][=action.done.for.sb.else]" : "@EP"[{vint}]
| "[--][-vent][+APPL.BEN][=action.done.for.sb.else]" : "@EP"[{vent}]
];
                   
define APPLBEN [ [EP V=S=APPLBEN]|[V=S=APPLBEN] ];
                   
# ****************************************
                               
define V=S=APPLPRES [
  "[--][-imo+company][+APPL.PRES][=with.other.person, presence]" : "@EP"[{imo}]
];
                   
define APPLPRES [ [EP V=S=APPLPRES]|[V=S=APPLPRES] ];
                   
# ****************************************
                   
define V=S=APPLSEP [
  "[--][-pitsa][+APPL.SEP][=separative]" : "@EP"[{pitsa}]
| "[--][-apitsa][+APPL.SEP][=separative]" : "@EP"[{apitsa}]
];
                   
define APPLSEP [ [EP V=S=APPLSEP]|[V=S=APPLSEP] ];
                   
# ****************************************
                   
# -------------------------------------
#            Reciprocal
# -------------------------------------
define V=S=RCP [
# ikaimavakaaka / i- =kaim -av -ak -aak -a / 3m.s- =to.call -rcp -caus.soc -prf -real / EN: 'they.called.to.each.other'  
  "[--][-av][+RCP]" : "@EP"[{av}]
];
                   
define RCP [ [EP V=S=RCP]|[V=S=RCP] ];
                   
# ****************************************
                   
# -------------------------------------
! opposite, reverse REV/CNTRST ; revocation?
define V=S=REV [
  "[--][-ry][+REV]" : "@EP"[{ry}] 
| "[--][-ri][+REV]" : "@EP"[{ri}] 
| "[--][-re][+REV]" : "@EP"[{re}] 
| "[--][-ree][+REV]" : "@EP"[{ree}]
];
                   
define REV [ [EP V=S=REV]|[V=S=REV] ];
                   
# ****************************************
                   
# 
define V=S=DUB [
  "[--][-ma][+DUB][=maybe; sb.might.do.sth.]" : [{ma}] ! +evidential 
| "[--][-mpa][+DUB][=maybe; sb.might.do.sth.]" : [{mpa}] ! +evidential 
| "[--][-npa][+DUB][=maybe; sb.might.do.sth.]" : [{npa}] ! +evidential 
# irirotaima iriri / =iriro -taima ir- =iri / =he -dub 3m.poss- =father / ES: 'tal vez puede ser el rey/padre [de los loros]' 
# aisatzitaima / =aisatzi -taima / =also -dub / EN: '' 
| "[--][-taima][+DUB][=maybe]" : [{taima}] 
];
                   
# ****************************************
                   
# -----------------------------------------------
# adverbial suffixes of time 
# -----------------------------------------------
define V=S=before [ # revise!
  "[--][-it+before]" : "@EP"[{it}]
];
                   
# ****************************************
                   
define V=S=early [ # revise!
  "[--][-aman][+early]" : "@EP"[{aman}]
];
                   
# ****************************************
                   
!define V=S=quickly [ # revise!
!  "[--][-ite][+quickly]" : "@EP"[{ite}]
!];
                   
# ****************************************
                   
# It needs to be fixed. 
# COMPL completive aspect (on verbs)
# 1. it focuses on the thorough completion of an event.
# 2. It denotes complete affectedness of the patientive argument.
define V=S=COMPL [
  "[--][-ite][Asp][+COMPL][=quickly,surprisingly]" : "@EP"[{ite}]
];
                   
define COMPL [ [EP V=S=COMPL]|[V=S=COMPL] ];
                   
# ****************************************
                   
define V=S=ICPL [
! "[--][-ite][+COMPL][=quickly,surprisingly]" : "@EP"[{ite}]
  "[--][-it][+ICPL][=speed]" : "@EP"[{it}]
| "[--][-itz][+ICPL][=speed]" : "@EP"[{itz}]
];
                   
define ICPL [ [EP V=S=ICPL]|[V=S=ICPL] ];
                   
# -----------------------------------------------
# ****************************************
                   
define V=S=TEMPORAL [
  "[--][-ni][+TEMPORAL][=ceased.exist., deceased]" : "@EP"[{ni}]
];
                   
# ****************************************
                   
define V=S=smallpart [ # revise!
  "[--][-patsa+small.part]" : "@EP"[{patsa}]
];
                   
# ****************************************
                   
define V=S=DEGR [
  "[--][-piro][+DEGR][=indeed, verity]" : "@EP"[{piro}]
| "[--][-pero][+DEGR][=indeed, verity]" : "@EP"[{pero}]
];
                   
define DEGR [ [EP V=S=DEGR]|[V=S=DEGR] ]; 
                   
# ****************************************
                   
define V=S=small [ # revise!
  "[--][-kapichi+small]" : "@EP"[{kapichi}]
];
                   
# ****************************************
                   
!define V=S=1SGposs [
!  "[--][+1SG.poss]" : "@EP"[{no}|{n}]
!];
                   
# ****************************************
                   
!      define V=S=3SGmIRR [
!       "[--][+3SG.m.IRR]" : "@EP"[{ir}|{iri}]
!      ];
                   
# ****************************************
                   
! It signals the beginning phase of an activity/situation. 
define V=S=INCH [
# --------------------------------------------------------------------------
# Inceptive 
  "[--][-ima][Asp][+INCH][=beginning.of.a.new.state]" : "@EP"[{ima}]
# --------------------------------------------------------------------------
| "[--][-imai][Asp][+INCH][=beginning.of.a.new.state]" : "@EP"[{imai}]
| "[--][-imaint][Asp][+INCH][=beginning.of.a.new.state]" : "@EP"[{imaint}]
| "[--][-amaint][Asp][+INCH][=beginning.of.a.new.state]" : "@EP"[{amaint}]
];
                   
define INCH [ [EP V=S=INCH]|[V=S=INCH] ];
                   
# ****************************************
                   
!define V=S=flat [
!  "[--][+flat]" : "@EP"[{miront}]
!];
                   
# ****************************************
                   
!define V=S=REALEMPH [
!  "[--][-tzitzi][+REAL.EMPH]" : "@EP"[{tzitzi}]
!];
                   
# ****************************************
                   
!      define V=S=2S [
!       "[--][+2S]" : "@EP"[{pi}|{p}]
!      ];
                   
# ****************************************
                   
!      define V=S=3nmposs [
!       "[--][+3n.m.poss]" : "@EP"[{o}]
!      ];
                   
# ****************************************
                   
!      define V=S=1PLposs [
!       "[--][+1PL.poss]" : "@EP"[{a}]
!      ];
                   
# ****************************************
                   
define V=S=almost [ # revise! 
  "[--][-tya+almost]" : "@EP"[{tya}]
];
                   
# ****************************************
                   
# action finished a long time ago
# REM.PST: remote past  
define V=S=RPST [
  "[--][-ni][+R.PST]" : [{ni}]
];
                   
# ****************************************
                   
!define V=S=neck [
!  "[--][-kintsi+neck]" : "@EP"[{kintsi}]
!];
                   
# ****************************************
                   
# ---------------------------------------
#            Subordinator 
# ---------------------------------------
# 1. Temporal: EN: 'when'; ES: 'cuando' 
# 2. Location: EN: 'where'; ES: 'donde' 
# 3. Denoting a cause/motive/reason: EN: 'since'; ES: 'ya.que,entonces,pues' 
# pues: conj. Denota causa, motivo o razón, por ejemplo: 'háblale tú, pues lo conoces más que yo'
define V=S=SUB [ 
# pikantatziira / pi- =kant -atzi -i -ra / 2s- =to.be -prog -real -sub[temporal:when] / EN: 'when.you.were.saying'   
  "[--][-ra][+SUB][=Temporal:when;Location:where(place);Denoting.a.cause/motive/reason:since]" : [{ra}]
]; 
                   
# ****************************************
                   
define V=S=NEG [
# ero paminavaitzitzi / =ero *** p- =amin -a -vai -tz -i -tzi / =neg.irr *** 2a- =to.look -ep -dur -ep -real -neg / EN: "don't.look" 
  "[--][-tsi+future][+NEG]" : [{tsi}] 
| "[--][-tzi+future][+NEG]" : [{tzi}] 
# te inkantaje / te inkantahe / =te *** i- N- =kant -a -he / =neg.real *** 3m.s- =neg.real 3m.s- irr- =to.say -real -neg  / EN: 'invalid'; ES: 'inválido' 
| "[--][-he+past][+NEG]" : [{he}] 
| "[--][-hi+past][+NEG]" : [{hi}] 
];
                   
# ****************************************
                   
!      define V=S=real [
!       "[--][+real]" : "@EP"[{a}]
!      ];
                   
# ****************************************
                   
define V=S=RELIRR [
  "[--][-ni][+REL.IRR]" : [{ni}]
];
                   
# ****************************************
                   
! desirable extremal 
define V=S=DEXT [
  "[--][-asano+degree][+DEXT]" : "@EP"[{asano}]
];
                   
define DEXT [ [EP V=S=DEXT]|[V=S=DEXT] ];
                   
# ****************************************
                   
! position
define V=S=POSN [
  "[--][-atink][+POSN][=suspended.in.the.air, air.suspended]" : "@EP"[{atink}] 
| "[--][-ampe][+POSN][=in.circles]" : "@EP"[{ampe}]
];
                   
define POSN [ [EP V=S=POSN]|[V=S=POSN] ];
                   
# ****************************************
                   
! kisaneentantatsiri / =kis -a -neent -ant -atsi -ri / =to.be.angry -ep -s -ep -appl.reas -stat -rel / EN: 'resentful'; ES: 'rencoroso' 
! kisaneentantatsiro / =kis -a -neent -ant -atsi -ro / =to.be.angry -ep -s -ep -appl.reas -stat -3n.m.o / EN: 'resentful'; ES: 'rencorosa' 
! EN: 'sensory, sensorial'; ES: 'sensorial' 
define V=S=SENSORY [
  "[--][-nint][+SENSORY]" : "@EP"[{nint}] 
| "[--][-nent][+SENSORY]" : "@EP"[{nent}] 
| "[--][-neent][+SENSORY]" : "@EP"[{neent}] 
];
                   
define SENSORY [ [EP V=S=SENSORY]|[V=S=SENSORY] ];
                   
# ****************************************
                   
! referencial applicative 
define V=S=APPLREF [
  "[--][-imen][+APPL.REF][=concerning, relative.to, because.of]" : "@EP"[{imen}] 
| "[--][-iment][+APPL.REF][=concerning, relative.to, because.of]" : "@EP"[{iment}] 
];
                   
define APPLREF [ [EP V=S=APPLREF]|[V=S=APPLREF] ];
                   
# ****************************************
                   
define V=S=UNK1 [
  "[--][-aheempi][+UNK1][=when]" : "@EP"[{aheempi}] 
| "[--][-ahempi][+UNK1][=when]" : "@EP"[{ahempi}] 
];
                   
define UNK1 [ [EP V=S=UNK1]|[V=S=UNK1] ];
                   
# ****************************************
                   
define V=S=DLMT [
  "[--][-vintsa][+DLMT][=for.a.short.time]" : "@EP"[{vintsa}] 
];
                   
define DLMT [ [EP V=S=DLMT]|[V=S=DLMT] ];
                   
# ****************************************
                   
# new discovery 
# ----------- Mirativity --------------
# Reporting information which is new or surprising to the speaker 
define V=S=MIR [
  "[--][-atai][+MIR]" : "@EP"[{atai}] 
| "[--][-atae][+MIR]" : "@EP"[{atae}] 
];
                   
define MIR [ [EP V=S=MIR]|[V=S=MIR] ];
                   
# ****************************************
                   
# simulative suffix 
# Indicates feigned/simulated action.
define V=S=SIM [
  "[--][-amampe][+SIM]" : "@EP"[{amampe}] 
| "[--][-amampi][+SIM]" : "@EP"[{amampi}] 
];
                   
define SIM [ [EP V=S=SIM]|[V=S=SIM] ];
                   
# ****************************************
                   
define V=S=POSSREL [
  "[--][-nt][VS][+POSS.REL]" : [{nt}] 
];


      
# ##############################
#         Abbreviations         
# ##############################
# partic.		participant
# m.			masculine
# n.m.			not masculine
# 
# Asp			Aspectual morphology 
# E			Evidential 


