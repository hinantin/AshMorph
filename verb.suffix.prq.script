# I listed the suppletive allomorphs of each suffix.

define EP [
  "[--][-t][+EP]" : "@EP@"[{t}]
| "[--][-tz][+EP]" : "@EP@"[{tz}]
#EP1| "[--][<*-ts][+EP]" : "@EP@"[{ts}] ! disabled for debugging purposes 
| "[--][-a][+EP]" : "@EP@"[{a}]
];

# ****************************************
                   
source v-s-prog.foma 
                   
# ****************************************
                   
# O : transitive verbs 
# P : patient-like argument 
define V=S=PRNO=SET1 [
  "[--][=ri][+3m.O/P]" : [{ri}] 
| "[--][=re][+3m.O/P]" : [{re}] 
| "[--][=rii][+3m.O/P]" : [{rii}] 
| "[--][=ro][+3n.m.O/P]" : [{ro}] 
| "[--][=vo][+3n.m.O/P]" : [{vo}] 
];

# primera.persona, plural.inclusivo, objeto e
define V=S=PRNO=SET2 [
# ----------------------------------
  "[--][=ai][+1INCL.PL.O/P][=(to).us (ES: (a).nosotros,, nos)]" : "@EP"[{ai}] 
| "[--][=ae][+1INCL.PL.O/P][=(to).us (ES: (a).nosotros,, nos)]" : "@EP"[{ae}] ! \cite[pag. 465]{kindberg:diccionario:80} 
| "[--][=ee][+1INCL.PL.O/P][=(to).us (ES: (a).nosotros,, nos)]" : "@EP"[{ee}] ! \cite[pag. 465]{kindberg:diccionario:80} 
];
                   
define PRNO [ [V=S=PRNO=SET1] ];
                   
# ****************************************
                   
source v-s-dist.foma 
                   
# ****************************************
                   
source v-s-dir.foma 
                   
# ****************************************
                   
source v-s-prf.foma 
                   
# ****************************************
                   
# subjunctive clausal enclitic 
# SUBJUNCT, SUBJ  
# contingent action: contingent (on/upon something) (formal) depending on something that may or may not happen
# hypothetical action: based on situations or ideas which are possible and imagined rather than real and true
# ES: 'urgente, inminente' 
# apaata(tsita) / EN: 'wait!' 
define V=S=OPT [
  "[--][=ta][MODAL][+OPT_SUBJ][CONTG: actn.that.depends.on.sth.that.may.or.may.not.happen; HYPOTH: actn.that.is.possible.&.imagined.rather.than.real.&.true]" : [{ta}]
| "[--][=tsita][MODAL][+OPT_SUBJ][CONTG: actn.that.depends.on.sth.that.may.or.may.not.happen; HYPOTH: actn.that.is.possible.&.imagined.rather.than.real.&.true]" : [{tsita}]
];
                   
# ****************************************
                   
define V=S=PL=SET1 [
  "[--][-he][NUM][+PL][=PL.S/A+; INTR: PL.S/A+; TR: PL.S/A+.or.+PL.O]" : "@EP"[{he}] 
| "[--][-hei][NUM][+PL][=PL.S/A+; INTR: PL.S/A+; TR: PL.S/A+.or.+PL.O]" : "@EP"[{hei}] 
| "[--][-hee][NUM][+PL][=PL.S/A+; INTR: PL.S/A+; TR: PL.S/A+.or.+PL.O]" : "@EP"[{hee}] 
# -ite only appears in nominalized verb structures 
# ---------------------------------------
];

define PL=SET1 [ [EP V=S=PL=SET1]|[V=S=PL=SET1] ];

define V=S=PL=SET2=1 [
# ichiki / i- =chik -i / 3m.s- =to.cut -real / EN: 'he cut'
# ichikaiyini / i- =chik -aiy -i -ni / 3m.s- =to.cut -pl -real -pl+circumfixing / EN: 'they cut'
  "[--][-aiy][+PL]" : "@EP"[{aiy}] 
| "[--][-aey][+PL]" : "@EP"[{aey}] 
| "[--][-ayi][+PL]" : "@EP"[{ayi}] ! asheninka 
];

# According to the sample text corpus, this set doesn't use the epenthetic rules but contraction rules VOWEL+ey/+iy 
define V=S=PL=SET2=2 [
# Contraction process * more rules need to be added 
# tsirenitakoiyanakini / =tsireni -t -ako -iy -an -ak -i -ni /  
  "[--][-iy][+PL]" : [{iy}] 
| "[--][-ey][+PL]" : [{ey}] 
];
                   
define PL [ [EP V=S=PL=SET1]|[V=S=PL=SET1]|[EP V=S=PL=SET2=1]|[V=S=PL=SET2=1]|[V=S=PL=SET2=2] ];
                   
# ****************************************
                   
define V=S=PLcircumfixing [
  "[--][-ni+circumfixing][+PL]" : [{ni}]
| "[--][-ne+circumfixing][+PL]" : [{ne}]
];
                   
# ****************************************
                   
! I am taking Aspect instead of Cas as it is an analysis made specifically for Ashaninka.
! It specifies an event as a whole, that may be: 
! resolved.(event): finality-and-closure, once-and-for.all 
! regressive.(event): being.back 
define V=S=REGR [
# eiro aneahiritsi / =eiro ** a- =ne -ah -i -ri -tsi / =NEG.IRR ** 1pl.a- =to.see -term[finality-and-closure] -real -3m.o -neg / ; EN: 'we.won't.see.him.again'; ES: 'No.lo.veremos.otra.vez' 
  "[--][-a][ASP][+REGR_TERM][=REGR: to.repeat.sth.again, to.continue.sth.; TERM: finality-and-closure, once-and-for.all]" : "@EP"[{a}]
| "[--][-ah][ASP][+REGR_TERM][=REGR: to.repeat.sth.again, to.continue.sth.; TERM: finality-and-closure, once-and-for.all]" : "@EP"[{ah}]
| "[--][-aha][ASP][+REGR_TERM][=REGR: to.repeat.sth.again, to.continue.sth.; TERM: finality-and-closure, once-and-for.all]" : "@EP"[{aha}]
];
                   
define REGR [ [EP V=S=REGR]|[V=S=REGR] ];
                   
# ****************************************
                   
# Counterfactual 
define V=S=CNTF [
  "[--][=mi][MODAL][+CNT.F][=if-clause.expressing.an.idea/assumption/argument.contrary.to.fact (ES: no.realizado)]" : [{mi}]
| "[--][=me][MODAL][+CNT.F][=if-clause.expressing.an.idea/assumption/argument.contrary.to.fact (ES: no.realizado)]" : [{me}]
];
                   
# ****************************************

source v-s-realitystatussystem.foma

# ****************************************
                   
define V=S=AUG=SET1 [
# Intensifier 
  "[--][-piro][DEGR][+AUG][=VERITY:indeed, truly]" : "@EP"[{piro}]
| "[--][-pero][DEGR][+AUG][=VERITY:indeed, truly]" : "@EP"[{pero}]
| "[--][-pira][DEGR][+AUG][=VERITY:indeed, truly]" : "@EP"[{pira}]
];
                   
define AUG=SET1 [V=S=AUG=SET1]; 
                   
define V=S=AUG=SET2 [
# -------------------------------------------
  "[--][-ni][DEGR][+AUG][=truly]" : [{ni}]
];
                   
define AUG [ [EP V=S=AUG=SET1]|[V=S=AUG=SET1] ]; 
                   
define AUG=SET2 [V=S=AUG=SET2]; 
                   
define V=S=DEGR [
  "[--][-piro][+DEGR][=indeed, truly, verity]" : "@EP"[{piro}]
| "[--][-pero][+DEGR][=indeed, truly, verity]" : "@EP"[{pero}]
];
                   
define DEGR [ [EP V=S=DEGR]|[V=S=DEGR] ]; 
                   
# ****************************************
                   
! it specifies a situation which lasted for a period of time in the past.
!*) Nojate nomanaveete / Nohate nomanaveete / no- =ha -t -e  no- =mana -vee -t -e / 1sg.s- =to.go -ep -irr  1sg.s- =to.shoot -dur -ep -irr / EN: "I'm.going.to.hunt"; ES: 'yo.voy.a.cazar'  
! 
define V=S=DUR [
  "[--][-vai][ASP][+DUR][=action/state.endured.over.a.long.period.of.time; motion.took.place.over.a.long.distance]" : "@EP"[{vai}] 
| "[--][-vae][ASP][+DUR][=action/state.endured.over.a.long.period.of.time; motion.took.place.over.a.long.distance]" : "@EP"[{vae}] 
| "[--][-vee][ASP][+DUR][=action/state.endured.over.a.long.period.of.time; motion.took.place.over.a.long.distance]" : "@EP"[{vee}] 
| "[--][-vei][ASP][+DUR][=action/state.endured.over.a.long.period.of.time; motion.took.place.over.a.long.distance]" : "@EP"[{vei}] 
! Rio Envira, Rio Breu e Rio Amônea
! Owashiriwayta / Ovashirivayta / 
#| "[--][-vay][ASP][+DUR][=action/state..endured.over.a.long.period.of.time; motion.took.place.over.a.long.distance]" : "@EP"[{vay}] 
];
                   
define DUR [ [EP V=S=DUR]|[V=S=DUR] ];
                   
# ****************************************
                   
!define V=S=BEN [
!  "[--][-vint][+BEN][=action.done.for.sb.else]" : "@EP"[{vint}]
!| "[--][-vent][+BEN][=action.done.for.sb.else]" : "@EP"[{vent}]
!];
                   
# ****************************************
                   
!      define V=S=ADVP [
!       "[--][+ADV.P]" : "@EP"[{ranki}]
!      ];
                   
# ****************************************
                   
# ---------------------------------------------------------------
# subordinator 
# adverbializer: subordinating conjunction that links a subordinate clause to a main clause, and
# indicates that the subordinate clause has an "adverbial" or interpropositional relation to the main clause, indicating
# purpose, condition, time, and location. (SIL) 
define V=S=ADV=SET1 [
  "[--][-ka+proximal.loc.][+ADV]" : "@EP"[{ka}] 
| "[--][-ra+medial.loc.][+ADV]" : "@EP"[{ra}] 
| "[--][-nta+distal.loc.][+ADV]" : "@EP"[{nta}] 
# ---------------------------------------------------------------
| "[--][-ranki+fused.t.][Asp][+ADV.P][=past.situation]" : "@EP"[{ranki}] 
# ---------------------------------------------------------------
| "[--][-ni][AS][+ADV]" : "@EP"[{ni}] 
| "[--][-ini][AS][+ADV]" : "@EP"[{ini}] 
| "[--][-eni][AS][+ADV]" : "@EP"[{eni}] 
];
                   
define ADV [V=S=ADV=SET1];
                   
# ****************************************
                   
define V=S=ADV=SET2 [
  "[--][-paite][+ADV][=during, at.the.time.of.sth., period]" : "@EP"[{paite}] 
| "[--][-paiti][+ADV][=during, at.the.time.of.sth., period]" : "@EP"[{paiti}] 
];
                   
define ADV=SET2 [ [EP V=S=ADV=SET2]|[V=S=ADV=SET2] ];
                   
# ****************************************
                   
define V=S=O [
  "[--][=na][+1SG.O/P]" : [{na}] 
| "[--][=mi][+2SG.O/P]" : [{mi}] 
| "[--][=mpi][+2SG.O/P]" : [{mpi}] 
| "[--][=npi][+2SG.O/P]" : [{npi}] 
| "[--][=ni][+3SG.O/P]" : [{ni}] 
| "[--][=ne][+3SG.O/P]" : [{ne}] 
];
                   
# ****************************************
                   
! -kari EN: 'in.order.to.prevent.sth.from.happening'; ES: 'para.que.no; no.sea.(que); sino' 
! -kari EN: 'used.to.introduce.the.reason.for.the.particular.emotion.mentioned'; ES: 'con.el.fin.de' 
define V=S=APPR [
# omitsitsiyimikari / o- =mitsitsiy -i -mi -kari / 3n.m.s- = / ES: 'para.que.no.te.hundas'  
# *) pintzinai, anankorimikari shenotsi / pi- N- =tzin -a -i *** =anankori -mi -kari *** =shenotsi / 2s- irr- =to.rise -term -irr *** =to.cross -2sg.o -appr *** =goiter  
#    EN: 'get up! lest you cross the goiter'; ES: 'levántate, sino te va a cruzar la mamá del bocio' 
# *) Pipiakari / pi- =pi -a -kari / 2s- =to.lose -irr -appr / ES: '¡cuidado!, te.vas.a.perder'
  "[--][-kari][MODAL][+APPR][=lest (in.order.to.prevent; ES: ¡cuidado!)]" : [{kari}]
];
                   
# ****************************************
                   
!      define V=S=2A [
!       "[--][+2A]" : "@EP"[{pi}|{p}]
!      ];
                   
# ****************************************
                   
# nominalization/relativization markers
define V=S=REL [
  "[--][=ri+REAL][NS][+REL]" : [{ri}]
# -----------------------------------------------
| "[--][=ni+IRR][NS][+REL]" : [{ni}]
| "[--][=ni~ne+IRR][NS][+REL]" : [{ne}] ! =kaari =kemisan-t-atsi-ne ESP: 'terco', EN: 'stubborn' 
];
                   
# ****************************************
                   
# Manner suffix 
# *action/state* that was in some respect unsuccessful/interrupted, 
# the expected sequel to the *action* failed to obtain.
# 1) with no response 
# 2) without finishing 
define V=S=FRUS [
  "[--][-ve][+FRUS][=unsuccessful/interrupted.action/state (EN: in.vain; ES: casi); action.that.didn't.achieve.the.desired.result]" : "@EP"[{ve}]
| "[--][-vi][+FRUS][=unsuccessful/interrupted.action/state (EN: in.vain; ES: casi); action.that.didn't.achieve.the.desired.result]" : "@EP"[{vi}]
| "[--][-vit][+FRUS][=unsuccessful/interrupted.action/state (EN: in.vain; ES: casi); action.that.didn't.achieve.the.desired.result]" : "@EP"[{vit}]
# -----------------------------------------------
# ompiyamachevetajia / o- N=][=piya][VRoot][=to.defend (ES: defender)][--][-macheve][+FRUS]macheve[--][-t][+EP]t[--][-ah][Asp][+REGR_TERM][=REGR: to.repeat.sth.again, to.continue.sth.; TERM: finality-and-closure, once-and-for.all]ah[--][-ia][+IRR.RFL]ia
| "[--][-macheve][+FRUS]" : "@EP"[{macheve}]
];
                   
define FRUS [ [EP V=S=FRUS]|[V=S=FRUS] ];
                   
# ****************************************
                   
# instrumental applicative 
# derivational morpheme (VDeriv) 
# indicate a justification (JUSTIF): 
# infinitives of purpose (to explain why we do sth.) I went home to eat. / I made this video for you to learn.
# * the reason that 
# * for a reason 
# * for some reason 
# -ant .. =ri / EN: "that.with.which.sb.does.sth.; sb's.tool" 
# obantari / ovantari / EN: 'that.with.which.sb.eats.sth.'; ES: 'recipiente' 
# oantari / EN: 'that.with.which.sb.eats.sth.'; ES: 'receptáculo,recipiente' 
# noquenqueshireantari / nokenkeshireantari / no- =kenkeshire -ant -a =ri / ES: 'mi.idea' 
# noshiriaatantari / no- =shiriaa -t -ant -a =ri / ES: 'mi.canasta.para.pescar' 
# naneencantari / naneenkantari / n- =aneenk -ant -a =ri / ES: 'mi.aliento' 
# ocajantari / ocahantari / o- =cah -ant -a =ri / ES: 'recipiente.para.líquidos'
# nonantari / no- =n -ant -a =ri / ES: 'mi.casa' 
# akooyatantari / a- =kooya -t -ant -a =ri / EN: 'female.parts, female.genitalia' 
# ashiramparitantari / a- =shirampari -t -ant -a =ri / EN: 'male.parts, male.genitalia' 
# amatsatantyari / a- =matsa -t -ant -ya =ri / ES: 'adelgazante' 
# isankenatantyari / i- =sankena -t -ant -ya =ri / ES: 'lapicero' 
# akijantyari / akihantyari / a- =kiha -/DEL:a/nt -ya =ri / ES: 'cargador' 
# abantyari / a =/DEL:o/v -ant -ya =ri / ES: 'mesa.para.comer' 
# ipinayeetantari / i- =pina -yee -t -ant -a =ri / ES: 'recibo' 
# itimantari Jesoshi / i- =tim -ant -a =ri =Jesoshi / ES: 'su.nacimiento.de.Jesús, navidad' 
# inkiantyari / i- N= =ki -ant -ya =ri / ES: 'hueco.que.deja.entrar.el.agua.y.los.peces' 
# itsivacantari oorya / itsivakantari oorya / i- =tsivak -ant -a =ri *** =oorya / ES: 'cuando.el.sol.se.oscurecio; lit.: su.apagón.del.sol' 
define V=S=APPLREAS [
# Characteristic (CHAR) 
  "[--][-ant][Asp][+APPL.REAS][=in.order.to, so.that (ES: para.que)]" : "@EP"[{ant}]
| "[--][-anta][Asp][+APPL.REAS][=in.order.to, so.that (ES: para.que)]" : "@EP"[{anta}]
];
                   
define APPLREAS [ [EP V=S=APPLREAS]|[V=S=APPLREAS] ];
                   
# ****************************************
                   
define V=S=HAB [
  "[--][-apin][Asp][+HAB_CUST][=always]" : "@EP"[{apin}] 
| "[--][-apiin][Asp][+HAB_CUST][=always]" : "@EP"[{apiin}] 
| "[--][-apinin][Asp][+HAB_CUST][=always]" : "@EP"[{apinin}] 
| "[--][-apini][Asp][+HAB_CUST][=always]" : "@EP"[{apini}]
];
                   
define HAB [ [EP V=S=HAB]|[V=S=HAB] ];
                   
# ****************************************
                   
# --------------------------------------
#           Impersonal Passive 
# --------------------------------------
# DISTN: distance  ???
define V=S=IMPP [
# ichikaitakiro / i- =chik -ai -t -ak -i -ro / 3m.a- =to.cut -imp.p -ep -prf -real -3n.m.o / EN: 'someone.cut.it; it.was.cut'  
# hiitaitziri / h- =ii -t -ai -tz -i -ri / 3m.a- =to.be.called -ep -imp.p -ep -real -3m.o / EN: 'that.which.is.named'  
# impishitaitiro / i- N- =pishi -t -ai -t -i -ro / 3m.a- irr- =to.sweep -ep -imp.p -ep -real -3n.m.o / EN: 'someone.swept.it; it.was.swept'  
# ikitsataitakina / i- =kitsa -t -ai -t -ak -i -na / 3m.a- =to.dress -ep -imp.p -ep -prf -real -1sg.o / EN: 'they.dressed.me; I.was.dressed' 
# ishinkitaitakiri / i- =shinki -t -ai -t -ak -i -ri / 3m.a- =to.get.drunk -ep -imp.p -ep -prf -real -3m.o / EN: 'they.made.him.drunk', es: 'le.hicieron.emborrachar'  
# Only with 3rd person ???  
  "[--][-ai][+IMP.P][=3(PL)m.S/A+.is.unknown/unspecified-and-focus.on.+O/P]" : "@EP"[{ai}] 
| "[--][-ae][+IMP.P][=3(PL)m.S/A+.is.unknown/unspecified-and-focus.on.+O/P]" : "@EP"[{ae}] ! yet to be tested 
# yanteetakena / y- =ant -ee -t -ak -e =na / EN: 'I.was.beaten.by.them'; ES: 'me.han.pegado' 
# yanteetakeri  / y- =ant -ee -t -ak -e =ri / ES: 'he.was.beaten.by.them'; ES: 'le.han.pegado'
# yanteetakero  / y- =ant -ee -t -ak -e =ro / ES: 'she.was.beaten.by.them'; ES: 'le.han.pegado'
# yanteetapinintiri / y- =ant -ee -t -apinin -t -i =ri / EN: 'he.is.always.beaten.by.them'; ES: 'siempre.le.pegan'
# yoyeetari / y- =oy -ee -t -a =ri / 3m.s- =to.eat -imp.p -ep -irr =rel / EN: edible , lit.: 'that.is.eaten.by.him/them'; ES: 'comestible' 
# ijokeetakeri / ihokeetakeri / i- =hok -ee -t -ak -e =ri / EN: 'he.was.abandoned.by.them'; ES: 'le.han.abandonado-él' 
# ijokeetakero / ihokeetakero / i- =hok -ee -t -ak -e =ro / EN: 'she.was.kicked.out.of.a.job.by.him/them'; ES: 'le.han.botado.de.un.trabajo-ella' 
# imaninteetakeri / EN: 'he.was.scorned.by.them'; ES: 'le.han.despreciado-él' 
| "[--][-ee][+IMP.P][=3(PL)m.S/A+.is.unknown/unspecified-and-focus.on.+O/P]" : "@EP"[{ee}] 
];
                   
define IMPP [ [EP V=S=IMPP]|[V=S=IMPP] ];
                   
# ****************************************
                   
# *) -pameneribe jipajitari yora? / -pamenerive hipahitari yora? / p- =amen -e -ri -ve *** hi- =pahi -t -a -ri *** =yora+m.+medial
#     2a- =to.look -real -3m.o =excl[exclaiming] *** 3m.a- =to.be.called -ep -irr -3m.o *** =that / EN: "look, what's that?"  
# *) – Opaamakitaki mapira, katsirinivee! / o- =paamaki -t -ak -i *** =mapi -ra *** =kats -i =ri =ni =vee / EN: "– The stone is hot, it hurts!"
define V=S=EXCL [
  "[--][=ve][MODAL][+EXCL][=exclaiming,announcing]" : [{ve}]
| "[--][=vee][MODAL][+EXCL][=exclaiming,announcing]" : [{vee}]
| "[--][=vi][MODAL][+EXCL][=exclaiming,announcing]" : [{vi}]
];
                   
# ****************************************
                   
source v-s-stat.foma 
                   
# ****************************************
                   
define V=S=Q [
# ampokaika / a- N- =pok -ai -ka / 1pl.s- irr- =to.come -real -q / EN: 'we will come back (when)?'  
  "[--][=ka][+Q]" : [{ka}]
];
                   
# ****************************************
                   
# generalized applicative (GEN.APPL): with verbs of perception, cognition, or emotion, the indirective indicates that the subject perceives, thinks or has feelings about the applied object. , but that the applied object is not the direct object of the perception, thought, or feeling expressed by the verb root.
# indirective applicative (INDR.APPL):  action of the verb affects the applied object in an indirect manner
# derivational morpheme (VDeriv) 
define V=S=APPL [
  "[--][-ako][VS][+APPL.GEN][=GEN: with.reference.to.sth., about.sth.; INDR: action.affects.sth.in.an.indirect.manner]" : "@EP"[{ako}]
];
                   
define APPL [ [EP V=S=APPL]|[V=S=APPL] ];
                   
# ****************************************
                   
define V=S=ABS [ 
  "[--][-tsi+alien.][Gen][+nposs]" : [{tsi}] 
| "[--][-ntsi+alien.][Gen][+nposs]" : [{ntsi}] 
| "[--][-tzi+alien.][Gen][+nposs]" : [{tzi}] 
];
                   
# ****************************************
                   
define V=S=COND [
  "[--][=rika][MODAL][+COND][=if (expresses.possible.condition); like (comparison)]" : [{rika}] ! +evidential 
# This suffix indicates, for good/bad, an action or idea wasn't fulfilled.
# Also abbreviated CNT.EXP 'counter-expectational' 
| "[--][=me][MODAL][+SUBJ][=not.to.achieve/realize.an.action/idea (ES: para.bien/mal.las.acciones/ideas.expresadas.no.se.realizaron)]" : [{me}] 
];
                   
# ****************************************
                   
source v-s-nmz.foma 
                   
# ****************************************
                   
source v-s-adj.foma 
                   
# ****************************************
                   
# --------Sociative-Causative ----------
# Sociative causation is a particular type of causation, where the causer not only
# makes the causee do an action, but also participates in it. \cite{guillaume:sociative:10}
define V=S=CAUSSOC=SET1 [
  "[--][-ak][+APPL.CAUS.SOC][=A+(causer).causes.sth/sb(causee).to.do.sth.&.A+.also.participates.in.the.actn.; to.make.sth/sb.do.sth.]" : "@EP"[{ak}]
!| "[--][-ag][+APPL.CAUS.SOC][=A+(causer).causes.sth/sb(causee).to.do.sth.&.A+.also.participates.in.the.actn.; to.make.sth/sb.do.sth.]" : "@EP"[{ag}] ! to be revised 
| "[--][-aka][+APPL.CAUS.SOC][=A+(causer).causes.sth/sb(causee).to.do.sth.&.A+.also.participates.in.the.actn.; to.make.sth/sb.do.sth.]" : "@EP"[{aka}]
| "[--][-akaa][+APPL.CAUS.SOC][=A+(causer).causes.sth/sb(causee).to.do.sth.&.A+.also.participates.in.the.actn.; to.make.sth/sb.do.sth.]" : "@EP"[{akaa}]
];

define V=S=CAUSSOC=SET2 [
  "[--][-akag][+APPL.CAUS.SOC][=A+(causer).causes.sth/sb(causee).to.do.sth.&.A+.also.participates.in.the.actn.; to.make.sth/sb.do.sth.]" : "@EP"[{akag}] ! to be revised 
];
                   
define CAUSSOC [ [EP V=S=CAUSSOC=SET1]|[V=S=CAUSSOC=SET1] ];
                   
# ****************************************
                   
!define V=S=APPLSOC [
!  "[--][-ak][+APPL.SOC]" : "@EP"[{ak}]
!];
                   
# ****************************************
                   
!define V=S=CAUSCOM [
!  "[--][-aka][+CAUS.COM]" : "@EP"[{aka}]
!];
                   
# ****************************************
                   
!define V=S=CAUS [
!  "[--][-ak][+CAUS]" : "@EP"[{ak}]
!| "[--][-aka][+CAUS]" : "@EP"[{aka}]
!];
                   
# ****************************************
                   
# derivational morpheme (VDeriv) 
# instrumental applicative 
define V=S=APPLINST [
  "[--][-ant][+APPL.INST][=reason,instrumentality;in.order.to]" : "@EP"[{ant}]
];
                   
define APPLINST [ [EP V=S=APPLINST]|[V=S=APPLINST] ];
                   
# ****************************************
                   
source v-s-semel.foma 
                   
# ****************************************
                   
define V=S=REP [
  "[--][-a][+REP][=again.and.again]" : "@EP"[{a}]
| "[--][-aa][+REP][=again.and.again]" : "@EP"[{aa}]
];
                   
define REP [ [EP V=S=REP]|[V=S=REP] ];
                   
# ****************************************
# -----------------------------------------------------
      
# to indicate intent 
# exclusive with reflexive -a/-aa 
# purpusive: action.done.with.some.purpose.in.view 
define V=S=APPLINT [
  "[--][-ashi][+APPL.INT][=purpose,(with.the).intent]" : "@EP"[{ashi}]
| "[--][-shi][+APPL.INT][=purpose,(with.the).intent]" : "@EP"[{shi}]
];
                   
define APPLINT [ [EP V=S=APPLINT]|[V=S=APPLINT] ];
                   
# ****************************************
                   
define V=S=LOC [
  "[--][-ki][+LOC]" : "@EP"[{ki}]
];
                   
# ****************************************
                   
define V=S=NCFOC [
  "[--][=tea][+N.C.FOC][=urgency, impatience, surprise, exasperation (non-contradictory.emphasis); almost (with.time.adverbs)]" : [{tea}]
| "[--][=tya][+N.C.FOC][=urgency, impatience, surprise, exasperation (non-contradictory.emphasis); almost (with.time.adverbs)]" : [{tya}]
];
                   
# ****************************************
                         
define V=S=OFOC [
  "[--][=kya][MODAL][+O.FOC]" : [{kya}] 
| "[--][=kia][MODAL][+O.FOC]" : [{kia}] 
| "[--][=kea][MODAL][+O.FOC]" : [{kea}] 
];
                   
# ****************************************
                         
!define V=S=poss [
!  "[--][+poss]" : "@EP"[{ni}|{te}]
!];
                   
# ****************************************
                         
define V=S=EMPH [
# tya: urgency, impatience, surprise, exasperation 
# aritya / =ari -tya / =PP -EMPH /  
  "[--][=tya][MODAL][+EMPH][=urgency, impatience, surprise, exasperation (non-contradictory.emphasis); almost (with.time.adverbs)]" : [{tya}]
| "[--][=tyaa][MODAL][+EMPH][=urgency, impatience, surprise, exasperation (non-contradictory.emphasis); almost (with.time.adverbs)]" : [{tyaa}]
| "[--][=tea][MODAL][+EMPH][=urgency, impatience, surprise, exasperation (non-contradictory.emphasis); almost (with.time.adverbs)]" : [{tea}]
! 
# Assertive 
| "[--][=kya][MODAL][+EMPH]" : [{kya}]
| "[--][=kia][MODAL][+EMPH]" : [{kia}]
| "[--][=kea][MODAL][+EMPH]" : [{kea}]
! 
| "[--][=poo][+EMPH]" : "@EP"[{poo}]
! 
| "[--][=ha][+EMPH][=contradictory.emphasis]" : "@EP"[{ha}]
! 
| "[--][=tzi][+EMPH]" : "@EP"[{tzi}]
! 
! ñ-ak-e-ro=sa / EN: 'you will see it' 
| "[--][=sa][+EMPH]" : "@EP"[{sa}]
];
                   
# ****************************************
                         
!define V=S=MCAUS [
!  "[--][-mi][+M.CAUS]" : "@EP"[{mi}]
!];
                   
# ****************************************
                         
!      define V=S=CAUSAGT [
!       "[--][+CAUS.AGT]" : "@EP"[{v}]
!      ];
                   
# ****************************************
                               
define V=S=APPLBEN [
# okimoshirebentakero oshinto / okimoshireventakero oshinto / o- =kimoshire -vent -ak -e =ro *** o- =shinto / ES: 'está.muy.orgullosa.por.su.hija' 
  "[--][-vint][+APPL.BEN][=action.done.for.sb.else]" : "@EP"[{vint}]
| "[--][-vent][+APPL.BEN][=action.done.for.sb.else]" : "@EP"[{vent}]
];
                               
define APPLBEN [ [EP V=S=APPLBEN]|[V=S=APPLBEN] ];
                               
# ****************************************
                               
# presencial applicative 
# derivational morpheme (VDeriv) 
define V=S=APPLPRES [
  "[--][-imo+company][VS][+APPL.PRES][=with.other.person, presence]" : "@EP"[{imo}]
];
                   
define APPLPRES [ [EP V=S=APPLPRES]|[V=S=APPLPRES] ];
                   
# ****************************************
                   
# derivational morpheme (VDeriv) 
# referential (RFR) 
define V=S=APPLSEP [
  "[--][-pitsa][VS][+APPL.SEP][=separative; RFR: actn.done.with.reference_to/around.sb.else]" : "@EP"[{pitsa}]
| "[--][-apitsa][VS][+APPL.SEP][=separative; RFR: actn.done.with.reference_to/around.sb.else]" : "@EP"[{apitsa}]
];
                   
define APPLSEP [ [EP V=S=APPLSEP]|[V=S=APPLSEP] ];
                   
# ****************************************
                   
# -------------------------------------
#            Reciprocal
# -------------------------------------
define V=S=RCP [
# ikaimavakaaka / i- =kaim -av -ak -aak -a / 3m.s- =to.call -rcp -caus.soc -prf -real / EN: 'they.called.to.each.other'  
  "[--][-av][+RCP]" : "@EP"[{av}]
];
                   
define RCP [ [EP V=S=RCP]|[V=S=RCP] ];
                   
# ****************************************
                   
# -------------------------------------
# reversative
! opposite, reverse REV/CNTRST ; revocation?
define V=S=REV [
# otevaryaanaque / otevaryaanake / =oteva -ry -a -an -ak -e / ES: 'él.rompió.la.rama' 
  "[--][-ry][+REV][=reversal/undoing.of.an.action/state]" : "@EP"[{ry}] 
| "[--][-ri][+REV][=reversal/undoing.of.an.action/state]" : "@EP"[{ri}] 
| "[--][-re][+REV][=reversal/undoing.of.an.action/state]" : "@EP"[{re}] 
| "[--][-ree][+REV][=reversal/undoing.of.an.action/state]" : "@EP"[{ree}] 
];
                   
define REV [ [EP V=S=REV]|[V=S=REV] ];
                   
# ****************************************
                   
# 
define V=S=DUB [
  "[--][=ma][MODAL][+DUB][=maybe; sb.might.do.sth.]" : [{ma}] ! +evidential 
| "[--][=mpa][MODAL][+DUB][=maybe; sb.might.do.sth.]" : [{mpa}] ! +evidential 
| "[--][=npa][MODAL][+DUB][=maybe; sb.might.do.sth.]" : [{npa}] ! +evidential 
# irirotaima iriri / =iriro -taima ir- =iri / =he -dub 3m.poss- =father / ES: 'tal vez puede ser el rey/padre [de los loros]' 
# aisatzitaima / =aisatzi -taima / =also -dub / EN: '' 
| "[--][=taima][MODAL][+DUB][=maybe]" : [{taima}] 
];
                   
# ****************************************
                   
# -----------------------------------------------
# adverbial suffixes of time 
# -----------------------------------------------
define V=S=ANT1 [ # revise!
# Ant1 ; past/non-past.action
  "[--][-it][Asp][+ANT][=past/non-past.action:before]" : "@EP"[{it}]
];
                   
define ANT1 [ [EP V=S=ANT1]|[V=S=ANT1] ];
                   
# ****************************************
                   
# TIME.MODAL 
define V=S=EARLY [ 
  "[--][-aman][ADV][+TIME.MODAL][=early]" : "@EP"[{aman}]
| "[--][-aman~man][ADV][+TIME.MODAL][=early]" : "@EP"[{man}] 
];
                   
define EARLY [ [EP V=S=EARLY]|[V=S=EARLY] ];
                   
# ****************************************
                   
!define V=S=ADVMANNER [ # revise!
!  "[--][-ite][ADV][+SPEED][=quickly,surprisingly]" : "@EP"[{ite}]
!];
                   
!define ADVMANNER [ [EP V=S=ADVMANNER]|[V=S=ADVMANNER] ];
                   
# ****************************************
                   
# It needs to be fixed. 
# CMPL completive aspect (on verbs)
# 1. it focuses on the thorough completion of an event.
# 2. It denotes complete affectedness of the patientive argument.
define V=S=CMPL [
  "[--][-ite][Asp][+CMPL][=completion.of.an.event]" : "@EP"[{ite}]
];
                   
define CMPL [ [EP V=S=CMPL]|[V=S=CMPL] ];
                   
# ****************************************
                   
# incompletive/circumstantial 
define V=S=ICPL [
  "[--][-it][Asp][+ICPL][=actn/event.that.was.begun.but.not.completed]" : "@EP"[{it}]
| "[--][-itz][Asp][+ICPL][=actn/event.that.was.begun.but.not.completed]" : "@EP"[{itz}]
| "[--][-it~et][Asp][+ICPL][=actn/event.that.was.begun.but.not.completed]" : "@EP"[{et}]
];
                   
define ICPL [ [EP V=S=ICPL]|[V=S=ICPL] ];
                   
# -----------------------------------------------
# ****************************************
                   
define V=S=TEMPORAL [
  "[--][-ni][+TEMPORAL][=ceased.exist., deceased]" : "@EP"[{ni}]
];
                   
# ****************************************
                   
define V=S=smallpart [ # revise!
  "[--][-patsa+small.part]" : "@EP"[{patsa}]
];
                   
# ****************************************
                   
define V=S=small [ # revise!
  "[--][-kapichi+small]" : "@EP"[{kapichi}]
];
                   
# ****************************************
                   
!define V=S=1SGposs [
!  "[--][+1SG.poss]" : "@EP"[{no}|{n}]
!];
                   
# ****************************************
                   
!      define V=S=3SGmIRR [
!       "[--][+3SG.m.IRR]" : "@EP"[{ir}|{iri}]
!      ];
                   
# ****************************************
                   
! It signals the beginning phase of an activity/situation. 
! counterexpectational ‘but’
define V=S=INCH [
# --------------------------------------------------------------------------
# Inceptive (INCP) 
  "[--][-ima][Asp][+INCH][=beginning.of.a.new.state; CNT.EXP:but]" : "@EP"[{ima}]
# --------------------------------------------------------------------------
| "[--][-imai][Asp][+INCH][=beginning.of.a.new.state; CNT.EXP:but]" : "@EP"[{imai}]
| "[--][-imain][Asp][+INCH][=beginning.of.a.new.state; CNT.EXP:but]" : "@EP"[{imain}]
| "[--][-imaint][Asp][+INCH][=beginning.of.a.new.state; CNT.EXP:but]" : "@EP"[{imaint}]
| "[--][-amaint][Asp][+INCH][=beginning.of.a.new.state; CNT.EXP:but]" : "@EP"[{amaint}]
];
                   
define INCH [ [EP V=S=INCH]|[V=S=INCH] ];
                   
# ****************************************
                   
define V=S=almost [ # revise! 
  "[--][-tya+almost]" : "@EP"[{tya}]
];
                   
# ****************************************
                   
# action finished a long time ago
# REM.PST: remote past  
define V=S=RPST [
  "[--][-ni][+REM.PST][=actn.finished.a.long.time.ago (ES: acción.terminada.hace.mucho.tiempo)]" : [{ni}]
];
                   
# ****************************************
                   
!define V=S=neck [
!  "[--][-kintsi+neck]" : "@EP"[{kintsi}]
!];
                   
# ****************************************
                   
# ---------------------------------------
#            Subordinator 
# ---------------------------------------
# 1. Temporal: EN: 'when'; ES: 'cuando' 
# 2. Location: EN: 'where'; ES: 'donde' 
# 3. Denoting a cause/motive/reason: EN: 'since'; ES: 'ya.que,entonces,pues' 
# pues: conj. Denota causa, motivo o razón, por ejemplo: 'háblale tú, pues lo conoces más que yo'
define V=S=SUB [ 
# pikantatziira / pi- =kant -atzi -i -ra / 2s- =to.be -prog -real -sub[temporal:when] / EN: 'when.you.were.saying'   
  "[--][=ra][+SUB][=Temporal:when;Location:where(place);Denoting.a.cause/motive/reason:since;Epist.uncertainty:Okay?]" : [{ra}]
]; 
                   
# ****************************************
                   
define V=S=NEG [
# ero paminavaitzitzi / =ero *** p- =amin -a -vai -tz -i -tzi / =neg.irr *** 2a- =to.look -ep -dur -ep -real -neg / EN: "don't.look" 
  "[--][=tsi+future][+NEG]" : [{tsi}] 
| "[--][=tzi+future][+NEG]" : [{tzi}] 
# te inkantaje / te inkantahe / =te *** i- N- =kant -a -he / =neg.real *** 3m.s- =neg.real 3m.s- irr- =to.say -real -neg  / EN: 'invalid'; ES: 'inválido' 
| "[--][=he+past][+NEG]" : [{he}] 
| "[--][=hi+past][+NEG]" : [{hi}] 
];
                   
# ****************************************
                   
!      define V=S=real [
!       "[--][+real]" : "@EP"[{a}]
!      ];
                   
# ****************************************
                   
!define V=S=RELIRR [
!  "[--][=ni][+REL.IRR]" : [{ni}]
!];
                   
# ****************************************
                   
! desirable extremal 
# 1) desirable 
# 2) loudable 
define V=S=DEXT [
  "[--][-asano][DEGR][+DEXT][=action/state.realized.to.a.very.high.degr. (EN: really)]" : "@EP"[{asano}]
| "[--][-sano][DEGR][+DEXT][=action/state.realized.to.a.very.high.degr. (EN: really)]" : "@EP"[{sano}]
];
                   
define DEXT [ [EP V=S=DEXT]|[V=S=DEXT] ];
                   
# ****************************************
                   
! position
define V=S=POSN [
  "[--][-atink][+POSN][=suspended.in.the.air, air.suspended]" : "@EP"[{atink}] 
| "[--][-ampe][+POSN][=in.circles]" : "@EP"[{ampe}]
];
                   
define POSN [ [EP V=S=POSN]|[V=S=POSN] ];
                   
# ****************************************
                   
! kisaneentantatsiri / =kis -a -neent -ant -atsi -ri / =to.be.angry -ep -s -ep -appl.reas -stat -rel / EN: 'resentful'; ES: 'rencoroso' 
! kisaneentantatsiro / =kis -a -neent -ant -atsi -ro / =to.be.angry -ep -s -ep -appl.reas -stat -3n.m.o / EN: 'resentful'; ES: 'rencorosa' 
! kitsonkaneentatsiri / kityonkaneentatsiri / =kityonka -neent -atsi =ri / ES: 'que tiene un poco de color rojo' 
! EN: 'sensory, sensorial'; ES: 'sensorial' 
define V=S=SENSORY [
  "[--][-nint][DEGR][+SENSORY][=DIM: almost,(do).a.little.bit]" : "@EP"[{nint}] 
| "[--][-nent][DEGR][+SENSORY][=DIM: almost,(do).a.little.bit]" : "@EP"[{nent}] 
| "[--][-neent][DEGR][+SENSORY][=DIM: almost,(do).a.little.bit]" : "@EP"[{neent}] 
];
                   
define SENSORY [ [EP V=S=SENSORY]|[V=S=SENSORY] ];
                   
!define V=S=DIMDEGR [
!  "[--][-nint][+DIM.DEGR][=almost,a.little.bit]" : "@EP"[{nint}]
!];
                   
!define DIMDEGR [ [EP V=S=DIMDEGR]|[V=S=DIMDEGR] ];
                   
!define V=S=DIM [ # revise!
!  "[--][-nint][ADV][+DIM.DEGR][=almost,a.little.bit (degree)]" : "@EP"[{nint}]
!| "[--][-ni][+DIM]" : "@EP"[{ni}] # they come from nominalized verb roots
!| "[--][-ini][+DIM]" : "@EP"[{ini}] # they come from nominalized verb roots
!];
                   
# ****************************************
                   
! referencial applicative 
define V=S=APPLREF [
  "[--][-imen][+APPL.REF][=concerning, relative.to, because.of]" : "@EP"[{imen}] 
| "[--][-iment][+APPL.REF][=concerning, relative.to, because.of]" : "@EP"[{iment}] 
];
                   
define APPLREF [ [EP V=S=APPLREF]|[V=S=APPLREF] ];
                   
# ****************************************
                   
define V=S=UNK1 [
  "[--][-aheempi][+UNK1][=when]" : "@EP"[{aheempi}] 
| "[--][-ahempi][+UNK1][=when]" : "@EP"[{ahempi}] 
];
                   
define UNK1 [ [EP V=S=UNK1]|[V=S=UNK1] ];
                   
# ****************************************
                   
# focalized terminative 
define V=S=TERMFOC [
  "[--][-ahant][+TERM.FOC][=emphasize.the.endpoint-and-finality.of.an.event]" : "@EP"[{ahant}] 
];
                   
define TERMFOC [ [EP V=S=TERMFOC]|[V=S=TERMFOC] ];
                   
# ****************************************
                   
# Delimitative 
# Delimitative aspect suffix 
define V=S=DLMT [
# ponya naminavintsatziri / =ponya *** n- =amin -a -vintsa -tz -i -ri / EN: 'I.was.watching.(for.a.little.while)' \cite{mihas:narrative:14} 
  "[--][-vintsa][Asp][+DLMT][=limitation.on.the.duration.of.the.state/activity (for.a.short_time/little_while)]" : "@EP"[{vintsa}] 
# amenamintsatziro / =amen -a -mintsa -tz -i =ro / ES: 'observando' \cite{mihas:anaani:11} 
# yamenamintsatashitana kapicheeni / y- =amen -a -mintsa -t -ashi -t -a =na *** =kapicheeni / ES: 'poco.me.miraba' \cite{mihas:anaani:11} 
| "[--][-mintsa][Asp][+DLMT][=limitation.on.the.duration.of.the.state/activity (for.a.short_time/little_while)]" : "@EP"[{mintsa}] 
];
                   
define DLMT [ [EP V=S=DLMT]|[V=S=DLMT] ];
                   
# ****************************************
                   
# new discovery 
# ----------- Mirativity --------------
# Reporting information which is new or surprising to the speaker 
define V=S=MIR [
  "[--][-atai][+MIR][=sudden.discovery.of.knowledge; -ri/REL:surprise.in.a.rhetorical.Q.]" : "@EP"[{atai}] 
| "[--][-atae][+MIR][=sudden.discovery.of.knowledge; -ri/REL:surprise.in.a.rhetorical.Q.]" : "@EP"[{atae}] 
];
                   
define MIR [ [EP V=S=MIR]|[V=S=MIR] ];
                   
# ****************************************
                   
# simulative suffix 
# Indicates feigned/simulated action.
define V=S=SIM [
  "[--][-amampe][+SIM]" : "@EP"[{amampe}] 
| "[--][-amampi][+SIM]" : "@EP"[{amampi}] 
];
                   
define SIM [ [EP V=S=SIM]|[V=S=SIM] ];
                   
# ****************************************
                   
define V=S=POSSREL [
  "[--][-nt][VS][+POSS.REL]" : [{nt}] 
];
                   
# ****************************************
                   
# partitive 
define V=S=PTV1 [
  "[--][-aaran][+PTV]" : "@EP"[{aaran}] 
];
                   
define PTV1 [ [EP V=S=PTV1]|[V=S=PTV1] ];
                   
define V=S=PTV2 [
  "[--][-itsi][+PTV]" : "@EP"[{itsi}] ! it is also known as distributive 
| "[--][-tsi][+PTV]" : "@EP"[{tsi}] 
| "[--][-iti][+PTV]" : "@EP"[{iti}] 
| "[--][-it][+PTV]" : "@EP"[{it}] 
!| "[--][-t][+PTV]" : "@EP"[{t}] 
];
                   
define PTV2 [ [EP V=S=PTV2]|[V=S=PTV2] ];
                   
# ****************************************

define V=S=URGENT [
# pireketi piarentsi / p- =ir -e =keti *** piarentsi 
  "[--][=keti][+URGENT][=urgency.of.an.actn.]" : [{keti}]
| "[--][=kete][+URGENT][=urgency.of.an.actn.]" : [{kete}]
];

# ****************************************

define V=S=CERTAINTY [
  "[--][=te][+CERTAINTY][=negative.affectivity]" : [{te}]
| "[--][=teme][+CERTAINTY][==negative.affectivity]" : [{teme}]
| "[--][=teeme][+CERTAINTY][==negative.affectivity]" : [{teeme}]
];

# ****************************************

# distance 
define V=S=DISTN [
  "[--][=ai][+DISTN][=to.specify.spatial.distance]" : [{ai}]
];

define DISTN [ [EP V=S=DISTN]|[V=S=DISTN] ];

# ****************************************
                   
define CL [ [EP V=S=CL]|[V=S=CL] ];
                   
# ****************************************
                   
define S=NOUN [ [EP V=S=noun]|[V=S=noun] ];
                   
# ****************************************
                   
define V=S=DEGREE [
  "[--][=ki][+DEGREE][=excessive.degree]" : "@EP"[{ki}]
];
                   
# ****************************************

source v-s-possind.foma 

# ****************************************

# ##############################
#         Abbreviations         
# ##############################
# partic.		participant
# m.			masculine
# n.m.			not masculine
# 
# Asp			Aspectual morphology 
# E			Evidential 


