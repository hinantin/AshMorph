# -------------------------------------------------------------
# "THE REALITY STATUS SYSTEM" or "REALIS/IRREALIS MOOD SYSTEM" 
# -------------------------------------------------------------
# 
# REALITY STATUS (or STATUS for short) is an obligatory inflectional category in Ashaninka
# the broader category is “modality” (reflect[ing] a range of speaker attitudes towards a given event) 

# -------------------------------------------
# "REALIS" 
# -------------------------------------------
# LBL1. TEMPORAL REFERENCE: NON/FUTURE (+N.FUT.) + NONREFLEXIVE 
# it describes "realized" situations 
# +tns@n.fut. 
# -------------------------------------------
# LBL2. 
# i-class verbs - “irreflexive” verbs - ACTIVE VOICE +C.I@act.v. 
# a-class verbs - “reflexive” verbs (RFL) - MIDDLE VOICE +C.A@mid.v. 
# -------------------------------------------
# LBL3. RSS 
#       0. POLARITY: 
#                    POSITIVE +/P
#                    NONE     0
# 
#       1. HYPOTHETICALITY: 
#                           ACTUAL (NON-HYPOTHETICAL)  A
#                           NONE                       0

define V=S=REAL [
  "[--][-i~e+tns@n.fut.+C.I@act.v.+RSS@00][MODALITY][+REAL][=after.-ak/-ah]" : "@EP"[{e}]
| "[--][-i~ee+tns@n.fut.+C.I@act.v.+RSS@00][MODALITY][+REAL][=after.-ak/-ah]" : "@EP"[{ee}]
# -------------------------------------------
| "[--][-i+tns@n.fut.+C.I@act.v.+RSS@00][MODALITY][+REAL]" : "@EP"[{i}]
| "[--][-i~ii+tns@n.fut.+C.I@act.v.+RSS@00][MODALITY][+REAL]" : "@EP"[{ii}] ! to be checked 
# -------------------------------------------
| "[--][-a+tns@n.fut.+C.A@mid.v.+RSS@00][MODALITY][+REAL]" : "@EP"[{a}]
| "[--][-a~aa+tns@n.fut.+C.A@mid.v.+RSS@00][MODALITY][+REAL]" : "@EP"[{aa}] ! not preceded by -a 
# -------------------------------------------
!| "[--][-ia+tns@n.fut.][MODALITY][+REAL]" : "@EP"[{ia}] ! to be checked 
!| "[--][-ia~ya+tns@n.fut.][MODALITY][+REAL]" : "@EP"[{ya}] ! to be checked 
!| "[--][-ia~yaa+tns@n.fut.][MODALITY][+REAL]" : "@EP"[{yaa}] ! to be checked 
];

# -------------------------------------------
# "REALIS REFLEXIVE" 
# -------------------------------------------
# NON/FUTURE + REFLEXIVE (RFL)

define V=S=REALRFL [
  "[--][-a+tns@n.fut.+C.A@mid.v.+RSS@00][MODALITY][+REAL]" : "@EP"[{a}]
];

# -------------------------------------------
# "IRREALIS" 
# -------------------------------------------
# LBL1. TEMPORAL REFERENCE: FUTURE (+FUT.) 
# ES: acción.no.empezada, estado/acción.incompletos \cite[pag. 465]{kindberg:diccionario:80} 
# EN: it describes "unrealized" situations 
#     +tns@fut. 
# -------------------------------------------
# LBL2. 
# i-class verbs - “irreflexive” verbs - ACTIVE VOICE 
#     +C.I@act.v. 
# a-class verbs - “reflexive” verbs (RFL) - MIDDLE VOICE 
#     +C.A@mid.v. 
# -------------------------------------------
# LBL3. RSS 
#       0. POLARITY: 
#                    NEGATIVE -/N
#                    NONE     0
# 
#       1. HYPOTHETICALITY: 
#                           HYPOTHETICAL    H    a clause that expresses a hypothetical state of affairs.
#                           CONDITIONAL     C    
#                           COUNTERFACTUAL  O    a construction that expresses a conditional relationship between two events that failed to be realized in the past.
#                           NONE            0
# 
#       2. SPEAKER-ORIENTED.MODALITY: 
#                                     IMPERATIVE                      I
#                                     POLITE DIRECTIVE (DIRECTIVES)   D
#                                     POLITE EXHORTATIVE              E
#                                     NONE                            0
#                                     PERMISIVES?
#                                     HORTATIVES?
# 
#       3. AGENT-ORIENTED.MODALITY:
#                                   OBLIGATION          O
#                                   NEED (NECESSITY)	N
#                                   NONE                0
#
#       4. PROSPECTIVENESS:
#                           PURPOSIVE                  P
#                           PROSPECTIVE-COMPLEMENT     C
#                           NONE                       0

define V=S=IRR [
  "[--][-e+tns@fut.+C.I@act.v.+RSS@00000][MODALITY][+IRR]" : "@EP"[{e}]
| "[--][-e~ee+tns@fut.+C.I@act.v.+RSS@00000][MODALITY][+IRR]" : "@EP"[{ee}]
| "[--][-e~ye+tns@fut.+C.I@act.v.+RSS@00000][MODALITY][+IRR]" : "@EP"[{ye}] ! \cite[pag. 3]{mihas:rflx:rzpr:17}
| "[--][-e~yi+tns@fut.+C.I@act.v.+RSS@00000][MODALITY][+IRR]" : "@EP"[{yi}]
# -------------------------------------------
# -mpaa: estado/acción.imcompletos \cite[pag. 465]{kindberg:diccionario:80}
# onkentantapempariri 
# nonijaapempari. / nonihaapempari. / n- =oniha -ap -empa =ri / ES: 'Voy.a.mostrarte.'
| "[--][-eNpa~empa+tns@fut.+C.A@mid.v.+RSS@00000][MODALITY][+IRR]" : "@EP"[{empa}]
| "[--][-eNpa~empaa+tns@fut.+C.A@mid.v.+RSS@00000][MODALITY][+IRR]" : "@EP"[{empaa}]
| "[--][-eNpa~eempa+tns@fut.+C.A@mid.v.+RSS@00000][MODALITY][+IRR]" : "@EP"[{eempa}]
# naabakaatiyempa / ES: 'me.voy.a.casar'
# naabakaatiyema / ES: 'me.voy.a.casar'
# nokibanaajitatiyempa / ES: 'voy.a.lavarme.los.dientes'
# ikibanaajitatiyempa / ES: 'él.se.va.a.lavarse.los.dientes'
# nopiyakobentatiyempa / ES: 'me.voy.a.vengar'
# opiyakobentatiyempa / ES: '(ella).se.va.a.vengar'
# pipiyakobentempa / ES: 'véngate'
# ampitsatatiyempa / ES: '(ella).va.arrugarse'
# naamabentatiyempa / ES: '(yo).estaré.prevenido'
# nobashiretatiyempa / ES: '(yo).estaré.triste'
# nokaatatiyempa / ES: '(yo.me).bañaré'
# yapatsayeetatiyempari / ES: '(él).le.va.abollar'
# noponayempa / ES: 'voy.acostarme'
# otsitinkatiyempa / ES: '(ella).se.va.a.tropezar'
# antibatatiyempa / ES: '(ella).se.va.a.tropezar'
# notsitinkatiyempa / ES: 'me.voy.a.tropezar'
# nantibatatiyempa / ES: 'me.voy.a.tropezar'
| "[--][-eNpa~yempa+tns@fut.+C.A@mid.v.+RSS@00000][MODALITY][+IRR]" : "@EP"[{yempa}]
| "[--][-eNpa~yema+tns@fut.+C.A@mid.v.+RSS@00000][MODALITY][+IRR]" : "@EP"[{yema}] ! shortened form ?
| "[--][-eNpa~yea+tns@fut.+C.A@mid.v.+RSS@00000][MODALITY][+IRR]" : "@EP"[{yea}] ! shortened form ? \cite[pag. 13]{mihas:rflx:rzpr:17}
# -------------------------------------------
!| "[--][-a+tns@fut.][MODALITY][+IRR]" : "@EP"[{a}]
!| "[--][-a~aa+tns@fut.][MODALITY][+IRR]" : "@EP"[{aa}] ! not preceded by -a 
# -------------------------------------------
| "[--][-e~i+tns@fut.+RSS@00000][MODALITY][+IRR]" : "@EP"[{i}]
| "[--][-e~ii+tns@fut.+RSS@00000][MODALITY][+IRR]" : "@EP"[{ii}] ! to be checked 
# -------------------------------------------
| "[--][-ia+tns@fut.+C.A@mid.v.+RSS@00000][MODALITY][+IRR]" : "@EP"[{ia}]
| "[--][-ia~ya+tns@fut.+C.A@mid.v.+RSS@00000][MODALITY][+IRR]" : "@EP"[{ya}]
| "[--][-ia~yaa+tns@fut.+C.A@mid.v.+RSS@00000][MODALITY][+IRR]" : "@EP"[{yaa}]
| "[--][-ia~ea+tns@fut.+C.A@mid.v.+RSS@00000][MODALITY][+IRR]" : "@EP"[{ea}] ! \cite[pag. 3]{mihas:rflx:rzpr:17} \cite[pag. 465]{kindberg:diccionario:80}
];

# -------------------------------------------
# "IRREALIS REFLEXIVE" 
# -------------------------------------------
# FUTURE + REFLEXIVE (RFL)

define V=S=IRRRFL [
  "[--][-ia+tns@fut.+C.A@mid.v.+RSS@00000][MODALITY][+IRR]" : "@EP"[{ia}]
# nontaiya / no- N- =ta -iya / 1sg.s- irr- =to.burn -irr.refl / ES: 'yo.me.quemaré' 
| "[--][-ia~iya+tns@fut.+C.A@mid.v.+RSS@00000][MODALITY][+IRR]" : "@EP"[{iya}]
| "[--][-ia~eya+tns@fut.+C.A@mid.v.+RSS@00000][MODALITY][+IRR]" : "@EP"[{eya}]
];

# -------------------------------------------
# "IRREALIS CPB" 
# -------------------------------------------
# NON/FUTURE + REFLEXIVE (RFL)

define V=S=IRRCPB [
! tya ~ txa , tyaa ~ txaa 
  "[--][-ya~xa+tns@fut.+C.A@mid.v.+RSS@00000][MODALITY][+IRR]" : [{xa}]
| "[--][-yaa~xaa+tns@fut.+C.A@mid.v.+RSS@00000][MODALITY][+IRR]" : [{xaa}]
! tye ~ txe , tyee ~ txee 
| "[--][-ye~xe+tns@fut.+C.A@mid.v.+RSS@00000][MODALITY][+IRR]" : [{xe}]
| "[--][-yee~xee+tns@fut.+C.A@mid.v.+RSS@00000][MODALITY][+IRR]" : [{xee}]
];
# -------------------------------------------
                   
define STATUS [
  [EP V=S=REAL]|[V=S=REAL]
| [EP V=S=IRR]|[V=S=IRR]
| [EP V=S=REALRFL]|[V=S=REALRFL]
| [EP V=S=IRRRFL]|[V=S=IRRRFL]
# ------------------------------------------
| [EP V=S=PRNO=SET2]|[V=S=PRNO=SET2]
| [EP V=S=IRRCPB] [V=S=IRRCPB]
];
